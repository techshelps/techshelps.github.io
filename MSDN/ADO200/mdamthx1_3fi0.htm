<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>AppendChunk and GetChunk Methods Example</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<h1><a name="mdmthappendchunkx"></a>AppendChunk and GetChunk Methods Example</h1>
<p>
This example uses the <b>AppendChunk</b> and <b>GetChunk</b> methods to fill an image field with data from another record.</p>
<pre><code>Public Sub AppendChunkX()

   Dim cnn1 As ADODB.Connection
   Dim rstPubInfo As ADODB.Recordset
   Dim strCnn As String
   Dim strPubID As String
   Dim strPRInfo As String
   Dim lngOffset As Long
   Dim lngLogoSize As Long
   Dim varLogo As Variant
   Dim varChunk As Variant
   
   Const conChunkSize = 100

   ' Open a connection.
   Set cnn1 = New ADODB.Connection
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   cnn1.Open strCnn
   
   ' Open the pub_info table.
   Set rstPubInfo = New ADODB.Recordset
   rstPubInfo.CursorType = adOpenKeyset
   rstPubInfo.LockType = adLockOptimistic
   rstPubInfo.Open "pub_info", cnn1, , , adCmdTable
   
   ' Prompt for a logo to copy.
   strMsg = "Available logos are : " &amp; vbCr &amp; vbCr
   Do While Not rstPubInfo.EOF
      strMsg = strMsg &amp; rstPubInfo!pub_id &amp; vbCr &amp; _
         Left(rstPubInfo!pr_info, InStr(rstPubInfo!pr_info, ",") - 1) &amp; _
         vbCr &amp; vbCr
      rstPubInfo.MoveNext
   Loop
   strMsg = strMsg &amp; "Enter the ID of a logo to copy:"
   strPubID = InputBox(strMsg)
   
   ' Copy the logo to a variable in chunks.
   rstPubInfo.Filter = "pub_id = '" &amp; strPubID &amp; "'"
   lngLogoSize = rstPubInfo!logo.ActualSize
   Do While lngOffset &lt; lngLogoSize
      varChunk = rstPubInfo!logo.GetChunk(conChunkSize)
      varLogo = varLogo &amp; varChunk
      lngOffset = lngOffset + conChunkSize
   Loop
   
   ' Get data from the user.
   strPubID = Trim(InputBox("Enter a new pub ID:"))
   strPRInfo = Trim(InputBox("Enter descriptive text:"))
   
   ' Add a new record, copying the logo in chunks.
   rstPubInfo.AddNew
   rstPubInfo!pub_id = strPubID
   rstPubInfo!pr_info = strPRInfo

   lngOffset = 0 ' Reset offset.
   Do While lngOffset &lt; lngLogoSize
      varChunk = LeftB(RightB(varLogo, lngLogoSize - lngOffset), _
         conChunkSize)
      rstPubInfo!logo.AppendChunk varChunk
      lngOffset = lngOffset + conChunkSize
   Loop
   rstPubInfo.Update
   
    ' Show the newly added data.
   MsgBox "New record: " &amp; rstPubInfo!pub_id &amp; vbCr &amp; _
      "Description: " &amp; rstPubInfo!pr_info &amp; vbCr &amp; _
      "Logo size: " &amp; rstPubInfo!logo.ActualSize

   ' Delete new record because this is a demonstration.
   rstPubInfo.Requery
   cnn1.Execute "DELETE FROM pub_info " &amp; _
      "WHERE pub_id = '" &amp; strPubID &amp; "'"

   rstPubInfo.Close
   cnn1.Close   

End Sub
</code></pre>
</font></BODY>
</HTML>
