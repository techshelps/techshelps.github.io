<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Prepared Property Example</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<h1><a name="mdpropreparedx"></a>Prepared Property Example</h1>
<p>
This example demonstrates the <b>Prepared</b> property by opening two <b>Command</b> objects—one prepared and one not prepared.</p>
<pre><code>Public Sub PreparedX()

   Dim cnn1 As ADODB.Connection
   Dim cmd1 As ADODB.Command
   Dim cmd2 As ADODB.Command
   Dim strCnn As String
   Dim strCmd As String
   Dim sngStart As Single
   Dim sngEnd As Single
   Dim sngNotPrepared As Single
   Dim sngPrepared As Single
   Dim intLoop As Integer

   ' Open a connection.
   strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   Set cnn1 = New ADODB.Connection
   cnn1.Open strCnn

   ' Create two command objects for the same
   ' command -- one prepared and one not prepared.
   strCmd = "SELECT title, type FROM titles ORDER BY type"
   
   Set cmd1 = New ADODB.Command
   Set cmd1.ActiveConnection = cnn1
   cmd1.CommandText = strCmd
      
   Set cmd2 = New ADODB.Command
   Set cmd2.ActiveConnection = cnn1
   cmd2.CommandText = strCmd
   cmd2.<i>Prepared</i> = True
   
   ' Set a timer, then execute the unprepared
   ' command 20 times.
   sngStart = Timer
   For intLoop = 1 To 20
      cmd1.Execute
   Next intLoop
   sngEnd = Timer
   sngNotPrepared = sngEnd - sngStart
   
   ' Reset the timer, then execute the prepared
   ' command 20 times.
   sngStart = Timer
   For intLoop = 1 To 20
      cmd2.Execute
   Next intLoop
   sngEnd = Timer
   sngPrepared = sngEnd - sngStart

   ' Display performance results.
   MsgBox "Performance Results:" &amp; vbCr &amp; _
      "   Not Prepared: " &amp; Format(sngNotPrepared, _
      "##0.000") &amp; " seconds" &amp; vbCr &amp; _
      "   Prepared: " &amp; Format(sngPrepared, _
      "##0.000") &amp; " seconds"
      
   cnn1.Close

End Sub</code></pre>
</font></BODY>
</HTML>
