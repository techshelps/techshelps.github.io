<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Clone Method Example (Visual Basic)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<h1><a name="mdmthclonex"></a>Clone Method Example (Visual Basic)</h1>
<p>
This example uses the <b>Clone</b> method to create copies of a <b>Recordset</b> and then lets the user position the record pointer of each copy independently.</p>
<pre><code>Public Sub CloneX()

   Dim arstStores(1 To 3) As ADODB.Recordset
   Dim intLoop As Integer
   Dim strSQL As String
   Dim strCnn As String
   Dim strMessage As String
   Dim strFind As String

   ' Assign SQL statement and connection string to variables.
   strSQL = "SELECT stor_name FROM Stores " &amp; _
      "ORDER BY stor_name"
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "

   ' Open recordset as a static cursor type recordset.
   Set arstStores(1) = New ADODB.Recordset
   arstStores(1).CursorType = adOpenStatic
   arstStores(1).LockType = adLockBatchOptimistic
   arstStores(1).Open strSQL, strCnn, , , adCmdText

   ' Create two clones of the original Recordset.
   Set arstStores(2) = arstStores(1).<i>Clone</i>
   Set arstStores(3) = arstStores(1).<i>Clone</i>

   Do While True

      ' Loop through the array so that on each pass, 
      ' the user is searching a different copy of the 
      ' same Recordset.
      For intLoop = 1 To 3

         ' Ask for search string while showing where 
         ' the current record pointer is for each Recordset.
         strMessage = _
            "Recordsets from stores table:" &amp; vbCr &amp; _
            "&nbsp; 1 - Original - Record pointer at " &amp; _
            arstStores(1)!stor_name &amp; vbCr &amp; _
            "&nbsp; 2 - Clone - Record pointer at " &amp; _
            arstStores(2)!stor_name &amp; vbCr &amp; _
            "&nbsp; 3 - Clone - Record pointer at " &amp; _
            arstStores(3)!stor_name &amp; vbCr &amp; _
            "Enter search string for #" &amp; intLoop &amp; ":"
         strFind = Trim(InputBox(strMessage))
         If strFind = "" Then Exit Do

         ' Find the search string; if there's no 
         ' match, jump to the last record.
         arstStores(intLoop).Filter = "stor_name &gt;= '" &amp; strFind &amp; "'"
         If arstStores(intLoop).EOF Then
            arstStores(intLoop).Filter = adFilterNone
            arstStores(intLoop).MoveLast
         End If

      Next intLoop

   Loop

   arstStores(1).Close
   arstStores(2).Close
   arstStores(3).Close

End Sub
</code></pre>
<h2>VBScript Version</h2>
<p>
Here is the same example written in VBScript to be used in an Active Server Page (ASP). To view this fully functional example, you need to create a system Data Source Name (DSN) called AdvWorks using the data source AdvWorks.mdb installed with IIS and located at C:\InetPub\ASPSamp\AdvWorks. This is a Microsoft Access database file. Use <b>Find</b> to locate the file Adovbs.inc and place it in the directory you plan to use. Cut and paste the following code to Notepad or another text editor and save it as Clone.asp. You can view the result in any client browser.</p>
<p>
To exercise the example, change the line <code>RsCustomerList.Source = "Customers"</code> to <code>RsCustomerList.Source = "Products"</code> to count a larger table.</p>
<pre><code>&lt;!-- #Include file="ADOVBS.INC" --&gt;
&lt;% Language = VBScript %&gt;
&lt;HTML&gt;&lt;HEAD&gt;
&lt;TITLE&gt;ADO Clone Method&lt;/TITLE&gt;
&lt;/HEAD&gt;&lt;BODY&gt; &lt;Center&gt;
&lt;H3&gt;ADO Clone Method&lt;/H3&gt;
&lt;!--- ADO Connection Object used to create recordset--&gt;
&lt;% 
'Create and open Connection object
Set OBJdbConnection = Server.CreateObject("ADODB.Connection") 
OBJdbConnection.Open "AdvWorks" 
'Create and open Recordset object
Set RsCustomerList = Server.CreateObject("ADODB.Recordset")
RsCustomerList.ActiveConnection = OBJdbConnection
RsCustomerList.CursorType = adOpenKeyset
RsCustomerList.LockType = adLockOptimistic
RsCustomerList.Source = "Customers"
RsCustomerList.Open
%&gt;
&lt;HR&gt;
&lt;!-- Loop through Customers Table, adding 1 to the Counter variable each pass --&gt;
&lt;% 
   Set MyRecordset = RSCustomerList.Clone
   Counter = 0
   Do Until MyRecordset.EOF
      Counter = Counter + 1
      MyRecordset.MoveNext
   Loop
%&gt;
&lt;!-- Display Results --&gt;
&lt;H3&gt;There Are &lt;%=Counter %&gt; Records in the Customers Table&lt;/H3&gt;
&lt;BR&gt;&lt;HR&gt;
&lt;H4&gt;Location of DSN Datbase&lt;/H4&gt;
&lt;%' Show location of DSN data source
Response.Write(OBJdbConnection)
%&gt;
&lt;HR&gt;&lt;/Center&gt;&lt;/BODY&gt;&lt;/HTML&gt;
</code></pre>
</font></BODY>
</HTML>
