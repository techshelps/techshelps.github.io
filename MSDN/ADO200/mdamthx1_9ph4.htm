<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Supports Method Example</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<h1><a name="mdmthsupportsx"></a>Supports Method Example</h1>
<p>
This example uses the <b>Supports</b> method to display the options supported by a recordset opened with different cursor types. The DisplaySupport procedure is required for this procedure to run.</p>
<pre><code>Public Sub SupportsX()

   Dim aintCursorType(4) As Integer
   Dim rstTitles As ADODB.Recordset
   Dim strCnn As String
   Dim intIndex As Integer

   ' Open connections.
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "

   ' Fill array with CursorType constants.
   aintCursorType(0) = adOpenForwardOnly
   aintCursorType(1) = adOpenKeyset
   aintCursorType(2) = adOpenDynamic
   aintCursorType(3) = adOpenStatic
   
   ' Open recordset using each CursorType and 
   ' optimitic locking. Then call the DisplaySupport 
   ' procedure to display the supported options.
   For intIndex = 0 To 3
      Set rstTitles = New ADODB.Recordset
      rstTitles.CursorType = aintCursorType(intIndex)
      rstTitles.LockType = adLockOptimistic
      rstTitles.Open "titles", strCnn, , , adCmdTable
      
      Select Case aintCursorType(intIndex)
         Case adOpenForwardOnly
            Debug.Print "ForwardOnly cursor supports:"
         Case adOpenKeyset
            Debug.Print "Keyset cursor supports:"
         Case adOpenDynamic
            Debug.Print "Dynamic cursor supports:"
         Case adOpenStatic
            Debug.Print "Static cursor supports:"
      End Select

      DisplaySupport rstTitles
      rstTitles.Close
   Next intIndex

End Sub

Public Sub DisplaySupport(rstTemp As ADODB.Recordset)

   Dim alngConstants(11) As Long
   Dim booSupports As Boolean
   Dim intIndex As Integer

   ' Fill array with cursor option constants.
   alngConstants(0) = adAddNew
   alngConstants(1) = adApproxPosition
   alngConstants(2) = adBookmark
   alngConstants(3) = adDelete
   alngConstants(4) = adFind
   alngConstants(5) = adHoldRecords
   alngConstants(6) = adMovePrevious
   alngConstants(7) = adNotify
   alngConstants(8) = adResync
   alngConstants(9) = adUpdate
   alngConstants(10) = adUpdateBatch
   
   For intIndex = 0 To 10
      booSupports = _
         rstTemp.Supports(alngConstants(intIndex))
      If booSupports Then
         Select Case alngConstants(intIndex)
            Case adAddNew
               Debug.Print "&nbsp;&nbsp; AddNew"
            Case adApproxPosition
               Debug.Print "&nbsp;&nbsp; AbsolutePosition and AbsolutePage"
            Case adBookmark
               Debug.Print "&nbsp;&nbsp; Bookmark"
            Case adDelete
               Debug.Print "&nbsp;&nbsp; Delete"
            Case adFind
               Debug.Print "&nbsp;&nbsp; Find"
            Case adHoldRecords
               Debug.Print "&nbsp;&nbsp; Holding Records"
            Case adMovePrevious
               Debug.Print "&nbsp;&nbsp; MovePrevious and Move"
            Case adNotify
               Debug.Print "&nbsp;&nbsp; Notifications"
            Case adResync
               Debug.Print "&nbsp;&nbsp; Resyncing data"
            Case adUpdate
               Debug.Print "&nbsp;&nbsp; Update"
            Case adUpdateBatch
               Debug.Print "&nbsp;&nbsp; batch updating"
         End Select
      End If
   Next intIndex

End Sub
</code></pre>
</font></BODY>
</HTML>
