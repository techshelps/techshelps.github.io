<HTML>
<HEAD><script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Unicast Manager Control Sample</TITLE></HEAD>

<BODY>

<OBJECT ID="NSUnicastMgr1" WIDTH=1 HEIGHT=1
 CLASSID="CLSID:A5CBEE43-6C2C-11CF-BCFD-00AA00C14806">
</OBJECT>

<CENTER><H1>Unicast Server Monitor Sample<BR CLEAR=ALL></H1></CENTER>
<b>This sample connects to the specified unicast server and displays current configuration settings and client information.</b>

<CENTER>
<TABLE><TR>
<TD>Server Name: <INPUT TYPE=TEXT NAME=UniServerName VALUE="LocalHost" SIZE=25></TD>
<TD><INPUT TYPE=BUTTON NAME=ConnectToUniServer VALUE="Connect"></TD>
<TD>Status: <INPUT TYPE=TEXT NAME=UniServerStatusBox VALUE="Ready" SIZE="15"></TD>
</TR></TABLE>
</CENTER>
<BR>

<B>Server Properties:</B> (At time of connection)
<CENTER>
<TABLE WIDTH="90%"><TR>
<TD>Max Clients: <INPUT TYPE=TEXT SIZE=10 NAME=MaxClientsBox></TD>
<TD>Max Bandwidth: <INPUT TYPE=TEXT SIZE=15 NAME=MaxBandwidthBox></TD>
<TD>Max Bit Rate: <INPUT TYPE=TEXT SIZE=15 NAME=MaxFileBitRateBox></TD>
</TR></TABLE>
</CENTER>
<BR>

<B>Event Monitoring:</B> (At time of connection)
<CENTER>
<TABLE WIDTH="90%"><TR>
<TD>Server Events: <INPUT TYPE=TEXT SIZE=10 NAME=ServerEventsBox></TD>
<TD>Admin Events: <INPUT TYPE=TEXT SIZE=10 NAME=AdminEventsBox></TD>
<TD>Alert Events: <INPUT TYPE=TEXT SIZE=10 NAME=AlertEventsBox></TD>
<TD>Client Events: <INPUT TYPE=TEXT SIZE=10 NAME=ClientEventsBox></TD>
</TR></TABLE>
</CENTER>
<BR>

<B>Clients:</B>
 Information about each client is listed dynamically as clients connect and disconnect from the control.
<CENTER>
<TEXTAREA ROWS=6 COLS=90% NAME=ClientBox></TEXTAREA>
</CENTER>
<BR>

<B>Authentication Components:</B>
These plug-in modules are used to authenticate a client's <i>User ID and Password</i>.
<CENTER>
<TEXTAREA ROWS=6 COLS=90% NAME=AuthenticatorBox></TEXTAREA>
</CENTER>
<BR>
 
<B>Event Notification & Authorization Components:</B>
These plug-in modules are used to monitor and control client access to NetShow content.
<CENTER>
<TEXTAREA ROWS=6 COLS=90% NAME=AuthorizerBox></TEXTAREA>
</CENTER>

<SCRIPT LANGUAGE="VBScript">
<!--

' This routine is called when the user clicks the Connect button 
Sub ConnectToUniServer_OnClick
    On Error Resume Next
    ResetTheForm
    ' Connection succeded
    NSUnicastMgr1.Connect UniServerName.Value
    If    Err.Number = 0 Then
          UniServerStatusBox.Value = "Connected"
          ShowProperties
          EnumerateClients
          AuthenticatorBox.Value = EnumerateModules ( NSUnicastMgr1.AuthenticationPlugins )
          AuthorizerBox.Value = EnumerateModules ( NSUnicastMgr1.EventPlugins )
     Else	
          MsgBox "Connection could not be made" & vbCrLf & Err.Description
          ServerStatusBox.Value = "Not Connected"
    End If
End Sub

Sub ResetTheForm
  UniServerStatusBox.Value = " "
  ServerEventsBox.Value = " " 
  AdminEventsBox.Value = " " 
  AlertEventsBox.Value = " " 
  ClientEventsBox.Value = " " 
  MaxClientsBox.Value = " "
  MaxBandwidthBox.Value = " "
  MaxFileBitRateBox.Value = " "
  ClientBox.Value = " "
  AuthenticatorBox.Value = " "
  AuthorizerBox.Value = " "
End Sub

' Called when a client connects to the server.
Sub NSUnicastMgr1_OnClientConnect(datetime, status, clientid, ipaddress, port)
    EnumerateClients
end sub

' Called when a client disconnects from a server.
Sub NSUnicastMgr1_OnClientDisconnect(datetime, status, clientid, ipaddress, port)
    EnumerateClients
end sub

Sub ShowProperties
    On Error Resume Next

    ' Update Max Clients display.
    If       NSUnicastMgr1.MaxClients = -1 then
             MaxClientsBox.Value = "No Limit"
    Else   MaxClientsBox.Value = NSUnicastMgr1.MaxClients
    End If

    ' Update Max Bandwidth & Max File Bit Rate display.
    MaxBandwidthBox.Value = KbpsLimit ( NSUnicastMgr1.MaxBandwidth )
    MaxFileBitRateBox.Value = KbpsLimit ( NSUnicastMgr1.MaxFileBitRate )

    ' Update events filter display.
    ServerEventsBox.Value = EventsBox ( NSUnicastMgr1.ServerEvents ) 
    AdminEventsBox.Value = EventsBox ( NSUnicastMgr1.AdminEvents ) 
    AlertEventsBox.Value = EventsBox ( NSUnicastMgr1.AlertEvents ) 
    ClientEventsBox.Value = EventsBox ( NSUnicastMgr1.ClientEvents ) 
End Sub

' Display Kbps values ( Max File Bit Rate & MaxBandwidth )
Function KbpsLimit ( KbpsValue )
    If       KbpsValue = -1 then
             KbpsLimit = "No Limit"
    Else  KbpsTempVar =  KbpsValue / 1024
             KbpsLimit = KbpsTempVar & " Kbps" 
    End If
End Function

' Display enabled/disabled state of events.
Function EventsBox ( EventsValue )
    If    EventsValue	Then
          EventsBox = "Enabled"
    Else
          EventsBox = "Disabled"		
    End If
End Function

' Update client connection display.    
Sub EnumerateClients
    On Error Resume Next
    If    NSUnicastMgr1.ClientEvents then
          ClientConnections = CInt(NSUnicastMgr1.Clients.Count)
          If    ClientConnections < 1 then ClientConnections = "No"
                ClientBox.Value = ClientConnections & " client(s) connected:" & vbCrLf & vbCrLf
                Set ClientsSnapshot = NSUnicastMgr1.Clients
                For Each Client in ClientsSnapshot 
                      ClientBox.Value = ClientBox.Value & "Client #:(" & CInt(Client.ClientID) & ")"
                      ClientBox.Value = ClientBox.Value & " - Viewing Title: " & Client.FileName
                      ClientBox.Value = ClientBox.Value & vbCrLf
                next
    Else
          ClientBox.Value =  "Client events are NOT being monitored" &  vbCrLf
    End If
End Sub

' Enumerate plug-in security components
Function  EnumerateModules ( ModuleVar )
  EnumerateModules =  ""
  For   i = 0 to CInt( ModuleVar.Count - 1 )
          EnumerateModules = EnumerateModules & ModuleVar.Item(i).Description & vbCrlf
      Next
End Function
	
-->
</SCRIPT>

</BODY>
</HTML>
