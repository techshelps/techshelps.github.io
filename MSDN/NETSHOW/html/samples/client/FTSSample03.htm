<HTML>
<HEAD><script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Receive Multicast with Sender Verification</TITLE></HEAD>

<BODY>
<H2>Receive Multicast with Sender Verification</H2>

<OBJECT ID="nsfile1" CLASSID="CLSID:26F24A93-1DA2-11D0-A334-00AA004A5FC5" ALIGN="RIGHT" HEIGHT="10" WIDTH="10">
<PARAM NAME="_Version" VALUE="65536">
<PARAM NAME="_ExtentX" VALUE="2646">
<PARAM NAME="_ExtentY" VALUE="1323">
<PARAM NAME="_StockProps" VALUE="0">
</OBJECT>

<FORM NAME="MyForm">
<INPUT TYPE="BUTTON" NAME="BtnReceiveFile" VALUE="Start Receive">
<INPUT TYPE="BUTTON" NAME="BtnCancelFile" VALUE="Cancel">
<INPUT TYPE="BUTTON" VALUE="About FTS" NAME="BtnAbout">

<P>To receive a multicast file transfer, click the <B>Start Receive</B> button. Cancel with the <B>Cancel</B> button. Start the receive file operation <I>before</I> starting the send file operation. Enter a valid multicast IP address for the  sender you want to receive from in the <b>Requested Sender IP address</b> input box below.

<P>Requested Sender IP address: <INPUT TYPE="TEXT" NAME="SendName"> 
<P>Actual Sender IP Address: <INPUT TYPE="TEXT" NAME="SenderIP">

<P>If the sender does not have the expected IP address, the transfer is cancelled.

<SCRIPT LANGUAGE="VBScript">
<!--

Sub BtnReceiveFile_OnClick
    If    (MyForm.SendName.Value = "") Then
          MsgBox "Please enter a valid IP address for the sender."
          Exit Sub
    End If
    MyForm.SenderIP.Value = ""
    call nsfile1.FtsRegisterStatusCallback(0, 0, 0, 0, 0, 0, 0, 0, 0, 12, 0)
    On Error Resume Next
    call nsfile1.FtsStartFileTransferReceive("239.230.50.50", 5050, "", "%TEMP%\", 0, 1025)
    If    Err.Number Then MsgBox "Start Error = " & ReturnError(Err.Number)
End Sub

Sub BtnCancelFile_OnClick
    call nsfile1.FtsCancelFileTransferReceive
    MsgBox "Transfer Cancelled. Error = " & ReturnError(nsfile1.Result)
End Sub

Sub nsfile1_FtsOnTransferStarted
    MsgBox "Transfer Started. Error = " & ReturnError(nsfile1.Result)
    SenIP = nsfile1.SourceAddress
    If    Not (SenIP = MyForm.SendName.Value) Then 
          MsgBox "Transfer Cancelled - Wrong Sender."
          MyForm.SenderIP.Value = nsfile1.SourceAddress
          call nsfile1.FtsCancelFileTransferReceive
    End If
End Sub

Sub nsfile1_FtsOnTransferCompleted
    MsgBox "Transfer Completed. Error = " & ReturnError(nsfile1.Result)
    MyForm.SenderIP.Value = nsfile1.SourceAddress
End Sub

Sub BtnAbout_OnClick
    call nsfile1.AboutBox()
End Sub

Function ReturnError(ByVal ErrNum)
    Select Case ErrNum
          Case 0
                ErrString = "No Error"
          Case 1
                ErrString = "Already Requested"
          Case -2147467260
                ErrString = "Cancelled"
          Case -2147467259
                ErrString = "Failed"
          Case -2147287038
                ErrString = "File Not Found"
          Case -2147024882
                ErrString = "Out Of Memory"
          Case -2147024809
                ErrString = "Invalid Parameter"
          Case -2147023436
                ErrString = "Timed Out"
          Case Else
                ErrString = "Unknown Error " & CStr(Hex(ErrNum))
    End Select
    ReturnError = ErrString
End Function

-->
</SCRIPT>

</BODY>
</HTML>
