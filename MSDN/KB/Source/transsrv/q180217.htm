

<HTML>
<HEAD>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>HOWTO: Control Server Location in a Visual Basic Client </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q180217">
<META NAME="KBModify" CONTENT="1998/02/26">
<META NAME="KBCreate" CONTENT="1998/01/30">
<META NAME="Keywords" CONTENT="TSrvDeploy">
<META NAME="KBArea" CONTENT="Support; KB; transsrv">
<META NAME="Description" CONTENT="  DCOM allows clients to create and use OLE servers located on other computers. One means for controlling the server location is through the settings in the client computer's registry. These settings can be configured using DCOM Config (Dcomcnfg.exe)...">
<META NAME="Product" CONTENT="transsrv">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Security" CONTENT="PUBLIC ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QBS0,QAPN,QAH4,QAY5,QARL,QANF,QAHE,QBVX,QAOE,QBV8,QAP2,QA9Q,QATX,QAH6,QAHT V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>HOWTO: Control Server Location in a Visual Basic Client</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  February 26, 1998</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q180217</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



<P><h3> </h3>
 
The information in this article applies to:

<UL><LI>Microsoft Transaction Server, versions 1.0 and 2.0
<LI>Microsoft Visual Basic Control Creation and Enterprise Editions for
   Windows, version 5.0
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
DCOM allows clients to create and use OLE servers located on other
computers. One means for controlling the server location is through the
settings in the client computer's registry. These settings can be
configured using DCOM Config (Dcomcnfg.exe), a Microsoft Transaction Server
client installation executable, or other setup programs.
<P>
However, a second and more flexible means of controlling server location is
to use the new CoCreateInstanceEx function provided by DCOM. The code
provided in the "More Information" section of this article wraps the
CoCreateInstanceEx function in CreateObjectEx (a Visual Basic function).
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
WARNING: ANY USE BY YOU OF THE CODE PROVIDED IN THIS ARTICLE IS AT YOUR OWN
RISK. Microsoft provides this code "as is" without warranty of any kind,
either express or implied, including but not limited to the implied
warranties of merchantability and/or fitness for a particular purpose.
<P>
To use CreateObjectEx, add a module to your Visual Basic project and insert
the following code into the module.
<P>
You may now call CreateObjectEx as follows:
<PRE>   Dim obj As Object
   Set obj = CreateObjectEx("MyServer.MyClass", "ServerMachineName")
</PRE>This will create an instance of MyServer.MyClass on the server.
<P>
Several formats are available for the arguments, and these are
described in detail in the following section of this article.
<P>
A third argument is available to control the binding type.
If you are using early binding, you may wish to use this syntax:
<PRE>   Dim obj as MyServer.MyClass
   Set obj = CreateObjectEx("MyServer.MyClass", "ServerMachineName", _
                                     EARLY_BINDING)

</PRE>The following code uses the DCOM CoCreateInstanceEx function to control the
server location.
<P>
Option Explicit
<P>
<PRE>   Private Type GUID
      Data1 As Long
      Data2 As Integer
      Data3 As Integer
      Data4(7) As Byte
   End Type

   Private Type COSERVERINFO
      dwReserved1 As Long  'DWORD
      pwszName As Long     'LPWSTR
      pAuthInfo As Long    'COAUTHINFO *
      dwReserved2 As Long  'DWORD
   End Type

   Type MULTI_QI
      pIID As Long   'const IID*
      pItf As Object 'Interface *
      hr As Long     'HRESULT
   End Type

   Public Enum BindingType
      EARLY_BINDING = 0
      LATE_BINDING = 1
   End Enum

   Public Enum CLSCTX
      CLSCTX_INPROC_SERVER = 1
      CLSCTX_INPROC_HANDLER = 2
      CLSCTX_LOCAL_SERVER = 4
      CLSCTX_REMOTE_SERVER = 16
      CLSCTX_SERVER = CLSCTX_INPROC_SERVER + CLSCTX_LOCAL_SERVER _
                        + CLSCTX_REMOTE_SERVER
      CLSCTX_ALL = CLSCTX_INPROC_SERVER + CLSCTX_INPROC_HANDLER _
                        + CLSCTX_LOCAL_SERVER + CLSCTX_REMOTE_SERVER
   End Enum

   Private Const IID_IUnknown As String = _
                            "{00000000-0000-0000-C000-000000000046}"
   Private Const IID_IDispatch As String = _
                            "{00020400-0000-0000-C000-000000000046}"

   Private Declare Function CLSIDFromString Lib "OLE32" _
            (ByVal lpszCLSID As Long, pclsid As GUID) As Long
   Private Declare Function CLSIDFromProgID Lib "OLE32" _
            (ByVal lpszProgID As Long, pclsid As GUID) As Long
   Private Declare Function CoCreateInstanceEx Lib "OLE32" _
            (rclsid As GUID, ByVal pUnkOuter As Long, _
            ByVal dwClsContext As Long, pServerInfo As COSERVERINFO, _
            ByVal cmq As Long, rgmqResults As MULTI_QI) As Long

   ' Public Function CreateObjectEx
   ' Creates an Instance of an OLE Server on a specified computer.
   '
   ' Arguments:
   '
   ' The Class argument specifies the OLE Server.
   ' There are two formats for the Class argument:
   '   1. PROGID: "Excel.Application"
   '   2. CLSID: "{00000010-0000-0010-8000-00AA006D2EA4}"
   ' If a ProgID is used, the client's registry is used to get the CLSID.
   '
   ' The Server argument specifies the server computer.
   ' There are two formats for the Server argument:
   '   1. UNC ("\\ServerName" or "ServerName")
   '   2. DNS ("server.sub.com" or "135.5.33.19")
   ' The Server argument is optional. If it is not supplied,
   ' the ole server is created on the client computer.
   '
   ' The Binding argument specifies the type of binding used by client.
   ' There are two values: EARLY_BINDING and LATE_BINDING.
   '
   ' If the client uses late binding (e.g. dim obj as Object),
   ' it must use the LATE_BINDING value or omit the Binding argument.
   ' If the client uses early binding (e.g. dim obj as MySvr.MyClass),
   ' it may use either LATE_BINDING or EARLY_BINDING.
   ' Some OLE servers will work only with LATE_BINDING, others
   ' only with EARLY_BINDING, and others with either binding type.
   ' OLE servers written in Visual Basic 5.0 support both binding types.
   '
   Public Function CreateObjectEx(ByVal Class As String, _
            Optional ByVal Server As String = "", _
            Optional Binding As BindingType = LATE_BINDING) As Object
   Dim rclsid As GUID
   Dim riid As GUID
   Dim hr As Long
   Dim ServerInfo As COSERVERINFO
   Dim mqi As MULTI_QI
   Dim Context As Long

   ' Convert IID string to binary IID
   If Binding = EARLY_BINDING Then
        hr = CLSIDFromString(StrPtr(IID_IUnknown), riid)
   Else
        hr = CLSIDFromString(StrPtr(IID_IDispatch), riid)
   End If
   If hr &lt;&gt; 0 Then Err.Raise hr

   'Setup the MULTI_QI structure.
   mqi.pIID = VarPtr(riid)

   'Convert provided CLSID or ProgID string into a binary CLSID
   If ((Left(Class, 1) = "{") And (Right(Class, 1) = "}") _
                    And (Len(Class) = 38)) Then
      hr = CLSIDFromString(StrPtr(Class), rclsid)
   Else
      hr = CLSIDFromProgID(StrPtr(Class), rclsid)
   End If
   If hr &lt;&gt; 0 Then Err.Raise hr

   'Decide on the appropriate context value.
   If Server = "" Then
      Context = CLSCTX_SERVER
   Else
      Context = CLSCTX_REMOTE_SERVER
   End If

   'Setup the COSERVERINFO structure.
   ServerInfo.pwszName = StrPtr(Server)

   ' Create an instance of the object
   hr = CoCreateInstanceEx(rclsid, CLng(0), Context, _
                            ServerInfo, CLng(1), mqi)
   If hr &lt;&gt; 0 Then Err.Raise hr

   Set CreateObjectEx = mqi.pItf

   End Function

</PRE><h2>REFERENCES</h2>
 
<P>
For additional information about Transaction Server, please see the
following Microsoft Web sites:
<P>
<PRE>   <B><A HREF="http://support.microsoft.com/support/default.asp">http://support.microsoft.com/support/default.asp</A></B>
   <B><A HREF="http://www.microsoft.com/transaction/">http://www.microsoft.com/transaction/</A></B>
</PRE> 
<PRE>Keywords          : TSrvDeploy
Version           : WINDOWS:5.0; WINNT:1.0,2.0
Platform          : WINDOWS winnt
Issue type        : kbhowto</PRE>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
================================================================================<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  February 26, 1998</FONT>
	<BR>
	<A HREF="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
