

<HTML>
<HEAD>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>HOWTO: Call WNetConnectionDialog1 and WNetDisconnectDialog1 </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q177698">
<META NAME="KBModify" CONTENT="1997/12/05">
<META NAME="KBCreate" CONTENT="1997/12/04">
<META NAME="Keywords" CONTENT="vb5all vb5howto">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  The functions WNetConnectionDialog1 and WNetDisconnectDialog1 are very useful for connecting and disconnecting from remote resources. The functions take parameters that allow you to change the default values of the connect and disconnect dialogs be...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QAK7,QAY5,QAI5,QAGI,QBWP,QDL9,QBWO,QBWN,QAB4,QBS0,QBV8,QAB9,QAA1,QBWQ,QATX V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>HOWTO: Call WNetConnectionDialog1 and WNetDisconnectDialog1</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  December 5, 1997</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q177698</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
 
The information in this article applies to:

<UL><LI>Microsoft Visual Basic Professional Edition for Windows, version 5.0
<LI>Microsoft Windows 95
<LI>Microsoft Windows NT versions 3.51, 4.0
<LI>Microsoft Win32 Software Development Kit (SDK)
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
The functions WNetConnectionDialog1 and WNetDisconnectDialog1 are very
useful for connecting and disconnecting from remote resources. The
functions take parameters that allow you to change the default values of
the connect and disconnect dialogs before calling them.
<P>
They also provide a common set of error-handling messages to the user
automatically.
<P>
This article demonstrates how to use some of these functions from 32-Bit
Visual Basic. These functions are available on both Windows NT and Windows
95.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
The hardest part of getting these functions working is getting the strings
and the NETRESOURCE structure setup in the CONNECTDLGSTRUCT. Fortunately,
there are only a few strings in the DISCDLGSTRUCT structure.

<OL><P><LI>Create a new project with two CommandButtons on the main form.

<P><LI>Name the buttons cmdConnectDlg and cmdDisconnectDlg.

<P><LI>Add a Textbox to the form and name it txtDisconnect.

<P><LI>Place the following code in the form:
<P>
   Option Explicit
<P>
<PRE>   Private Const CONNDLG_RO_PATH = &amp;H1    'Resource path should be
                                          'read -only
   Private Const CONNDLG_CONN_POINT = &amp;H2 'Netware-style movable
                                          'connection point enabled
   Private Const CONNDLG_USE_MRU = &amp;H4    'Use MRU combobox
   Private Const CONNDLG_HIDE_BOX = &amp;H8   'Hide persistent connect checkbox
   '/*
   ' * NOTE:  Set, at most, one of the below flags. If neither flag is
   ' *        set, then the persistence is set to whatever the user chose
   ' *        during a previous connection
   '
   Private Const CONNDLG_PERSIST = &amp;H10     'Force persistent connection
   Private Const CONNDLG_NOT_PERSIST = &amp;H20 'Persistent not allowed
   ' */

   Private Const DISC_UPDATE_PROFILE = &amp;H1  'Remove persistent connection
   Private Const DISC_NO_FORCE = &amp;H40       'Don't force the disconnect if
                                            'files are still open
   Private Const RESOURCETYPE_DISK = &amp;H1
   Private Const NO_ERROR = 0
   Private Const WN_SUCCESS = NO_ERROR

   Private Declare Function MapDrive Lib "mpr.dll" Alias _
      "WNetConnectionDialog1A" ( _
      lpConnectDlgStruct As Any) As Long
   Private Declare Function UnMapDrive Lib "mpr.dll" Alias _
      "WNetDisconnectDialog1A" ( _
      lpDiscDlgStruct As Any) As Long

   Private Type CONNECTDLGSTRUCT
      cbStructure As Long
      hwndOwner As Long
      lpConnRes As Long
      dwFlags As Long
      dwDevNum As Long
   End Type

   Private Type DISCDLGSTRUCT
      cbStructure As Long
      hwndOwner As Long
      lpLocalName As String
      lpRemoteName As String
      dwFlags As Long
   End Type

   Private Type NETRESOURCE
      dwScope As Long
      dwType As Long
      dwDisplayType As Long
      dwUsage As Long
      lpLocalName As Long
      lpRemoteName As String
      lpComment As Long
      lpProvider As Long
   End Type

   'Memory Management Routines &amp; Constants
   Private Const GMEM_FIXED = &amp;H0
   Private Const GMEM_ZEROINIT = &amp;H40
   Private Const GPTR = (GMEM_FIXED Or GMEM_ZEROINIT)
   Private Declare Function GlobalAlloc Lib "kernel32" ( _
      ByVal wFlags As Long, _
      ByVal dwBytes As Long) As Long
   Private Declare Function GlobalFree Lib "kernel32" ( _
      ByVal hMem As Long) As Long
   Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
      lpOut As Any, _
      lpIn As Any, _
      ByVal cbCopy As Long)

   Private Sub cmdConnectDlg_Click()
      Dim cs As CONNECTDLGSTRUCT, nr As NETRESOURCE, res As Long
      'Setup the NETRESOURCE structure
      nr.lpRemoteName = "\\servername\sharename"
      nr.dwType = RESOURCETYPE_DISK

      With cs 'Setup the connection dialog structure
         .cbStructure = LenB(cs)
         .hwndOwner = Me.hWnd
         .lpConnRes = GlobalAlloc(GPTR, LenB(nr))
         CopyMemory ByVal .lpConnRes, nr, LenB(nr)
         .dwFlags = CONNDLG_USE_MRU Or _
                    CONNDLG_HIDE_BOX Or _
                    CONNDLG_NOT_PERSIST
      End With

      res = MapDrive(cs) 'Call WNetConnectionDialog1
      If res = WN_SUCCESS Then
         MsgBox "MapDrive Succeeded."
         txtDisconnect.Text = Chr$(cs.dwDevNum + 64) &amp; ":"
      Else
         MsgBox "Error: " &amp; Err.LastDllError
      End If
      GlobalFree (cs.lpConnRes)
   End Sub

   Private Sub cmdDisconnectDlg_Click()
      Dim ds As DISCDLGSTRUCT, res As Long
      'Setup the disconnect dialog structure
      ds.cbStructure = LenB(ds)
      ds.hwndOwner = Me.hWnd
      ds.lpLocalName = txtDisconnect.Text
      ds.dwFlags = DISC_NO_FORCE Or DISC_UPDATE_PROFILE

      res = UnMapDrive(ds) 'Call WnetDisconnectDialog1
      If res = WN_SUCCESS Then
         MsgBox "UnMapDrive Succeeded."
      Else
         MsgBox "Error: " &amp; Err.LastDllError
      End If
   End Sub

</PRE><P><LI>Execute the program. Click the cmdConnectDlg button, and connect to a
   network share. The drive letter that is connected is placed in the
   txtDisconnect text box. Now click the cmdDisconnectDlg button. The
   mapped drive will be disconnected.

<P><LI>Click the cmdDisconnectDlg button again: On Windows NT, an error will be
   displayed and then the error value will be returned by the
   WNetDisconnectDialog1 function.

<P><LI>Click the cmdConnectDlg button, and pass an invalid server name to the
   dialog. Another error message will appear explaining the problem and,
   again, passing the error back to the program.
</OL> 
<PRE>Keywords          : vb5all vb5howto
Version           : WINDOWS:5.0,95; WINNT:3.51,4.0
Platform          : WINDOWS winnt
Issue type        : kbhowto</PRE>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
================================================================================<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  December 5, 1997</FONT>
	<BR>
	<A HREF="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
