

<HTML>
<HEAD>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>How to Manipulate Groups &amp; Items in Program Manager Using DDE </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q138800">
<META NAME="KBModify" CONTENT="1995/12/08">
<META NAME="KBCreate" CONTENT="1995/10/29">
<META NAME="Keywords" CONTENT="kbinterop kbprg kbcode kbhowto">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  This article shows by example how to manipulate groups and items in Program Manager by using DDE.  MORE INFORMATION  Program Manager has a DDE command-string interface that allows other applications to:   - Create, display, delete, and reload group...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QABO,QAH4,QAFF,QAGB,QAB4,QAAP,QBT6,QAPN,QBV4,QAUJ,QADO,QAMB,QAHP,QATX,QDL9 V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>How to Manipulate Groups &amp; Items in Program Manager Using DDE</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  December 8, 1995</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q138800</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
The information in this article applies to:

<UL><LI>Professional and Enterprise Editions of Microsoft Visual Basic,
   16-bit only, for Windows, version 4.0
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
This article shows by example how to manipulate groups and items in Program
Manager by using DDE.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
Program Manager has a DDE command-string interface that allows other
applications to:

<UL><LI>Create, display, delete, and reload groups.

<LI>Add items to groups.

<LI>Replace items in groups.

<LI>Delete items from groups.

<LI>Close Program Manager.
<P>
</UL>The following commands perform these actions:
<P>
<PRE>   CreateGroup
   Reload (Windows 3.1 only)
   DeleteGroup
   ShowGroup
   ReplaceItem (Windows 3.1 only)
   DeleteItem (Windows version 3.1 only)
   AddItem

</PRE><h3>Step-by-Step Example</h3>
 
<P>
Perform the following steps to produce an application that manipulates
Program Manager using DDE:

<OL><P><LI>Start a new project in Visual Basic. Form1 is created by default.

<P><LI>Add the following controls to Form1, setting properties as indicated:
<P>
<PRE>   Control                      Property   Value
   ----------------------------------------------------------------
   Text box (Text1)
   Label (Label1)               Caption    Group
   Text box (Text2)             Name       GGroup
   Label (Label2)               Caption    Item
   Text box (Text3)             Name       GItem
   Label (Label3)               Caption    Command Line
   Text box (Text4              Name       ItemExe
   Command button (Command1)    Name       CGroup (for create group)
   Command button (Command2)    Name       DGroup (for delete group)
   Command button (Command3)    Name       SGroup (for show group)
   Command button (Command4)    Name       Reload
   Command button (Command5)    Name       AItem (for add item)
   Command button (Command6)    Name       DItem (for delete item)
   Command button (Command7)    Name       RItem (for replace item)

</PRE><P><LI>Add the following code to the CGroup_Click event:
<P>
<PRE>   Private Sub CGroup_Click ()
   Dim cmd As String
      On Error GoTo CGError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      cmd = "[CreateGroup(" + GGroup.Text + ")]"
      text1.LinkExecute cmd
   CGDone:   text1.LinkMode = 0
      Exit Sub
   CGError:
      MsgBox "Error Adding Group"
      Resume CGDone
   End Sub

</PRE><P><LI>Add the following code to the DGroup_Click event:
<P>
<PRE>   Private Sub DGroup_Click ()
   Dim cmd As String
      On Error GoTo DGError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      cmd = "[DeleteGroup(" + GGroup.Text + ")]"
      text1.LinkExecute cmd
   DGDone:   text1.LinkMode = 0
      Exit Sub
   DGError:
      MsgBox "Error Deleting Group"
      Resume DGDone
   End Sub


</PRE><P><LI>Add the following code to the SGroup_Click event:
<P>
<PRE>   Private Sub SGroup_Click ()
      Dim cmd As String
      On Error GoTo SGError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      cmd = "[ShowGroup(" + GGroup.Text + ", 2" + ")]"
      text1.LinkExecute cmd
      cmd = "[ShowGroup(" + GGroup.Text + ", 1" + ")]"
      text1.LinkExecute cmd
   SGDone:
      text1.LinkMode = 0
      Exit Sub
   SGError:
      MsgBox "Error Showing Group"
      Resume SGDone
   End Sub

</PRE><P><LI>Add the following code to the Reload_Click event:
<P>
<PRE>   Private Sub Reload_Click ()
   Dim cmd As String
      On Error GoTo RLError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      cmd = "[Reload(" + GGroup.Text + ")]"
      text1.LinkExecute cmd
   RLDone:   text1.LinkMode = 0
      Exit Sub
   RLError:
      MsgBox "Error Reloading Group"
      Resume RLDone
   End Sub

</PRE><P><LI>Add the following code to the AItem_Click event:
<P>
<PRE>   Private Sub AItem_Click ()
   Dim cmd As String
      On Error GoTo AIError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      '*** The ShowGroup is necessary because AddItem changes the group
      '*** with the focus. ShowGroup gives the focus to the group where
      '*** you want the action taken.
      If (Len(GGroup.Text) &gt; 0) Then
         cmd = "[ShowGroup(" + GGroup.Text + ", 2" + ")]"
         text1.LinkExecute cmd
         cmd = "[ShowGroup(" + GGroup.Text + ", 1" + ")]"
         text1.LinkExecute cmd
      End If
      cmd = "[Additem(" + ItemExe.Text + "," + GItem.Text + ")]"
      text1.LinkExecute cmd
   AIDone:
      text1.LinkMode = 0
      Exit Sub
   AIError:
      MsgBox "Error adding Item"
      Resume AIDone
   End Sub

</PRE><P><LI>Add the following code to the DItem_Click event:
<P>
<PRE>   Private Sub DItem_Click ()
      Dim cmd As String
      On Error GoTo DIError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      '*** ShowGroup is necessary because DeleteItem changes the group
      '*** with the focus. ShowGroup gives the focus to the group where
      '*** you want the action taken.
      If (Len(GGroup.Text) &gt; 0) Then
         cmd = "[ShowGroup(" + GGroup.Text + ", 2" + ")]"
         text1.LinkExecute cmd
         cmd = "[ShowGroup(" + GGroup.Text + ", 1" + ")]"
         text1.LinkExecute cmd
      End If
      cmd = "[DeleteItem(" + GItem.Text + ")]"
      text1.LinkExecute cmd
   DIDone:   text1.LinkMode = 0
      Exit Sub
   DIError:
      MsgBox "Error Deleting Item"
      Resume DIDone
   End Sub

</PRE><P><LI>Add the following code to the RItem_Click event:
<P>
<PRE>   Private Sub RItem_Click ()
   Dim cmd As String
      On Error GoTo RIError
      text1.LinkMode = 0
      text1.LinkTopic = "Progman|Progman"
      text1.LinkMode = 2
      '*** ShowGroup gives the focus to the group where you want the action
      '*** taken.
      If (Len(GGroup.Text) &gt; 0) Then
         cmd = "[ShowGroup(" + GGroup.Text + ", 2" + ")]"
         text1.LinkExecute cmd
         cmd = "[ShowGroup(" + GGroup.Text + ", 1" + ")]"
         text1.LinkExecute cmd
      End If
      cmd = "[ReplaceItem(" + GItem.Text + ")]"
      text1.LinkExecute cmd
      cmd = "[Additem(" + ItemExe.Text + "," + GItem.Text + ")]"
      text1.LinkExecute cmd
   RIDone:
      text1.LinkMode = 0
      Exit Sub
   RIError:
      MsgBox "Error Replacing Item"
      Resume RIDone
   End Sub

</PRE><P><LI>On the Run menu, click Start (ALT, R, S) or press the F5 key to run the
   program. Enter the group you want created in the GGroup box, and click
   the Create Group button. You will now see the group you created in
   Program Manager. To add an item to a group, enter the group in the
   GGroup box. Enter the item you want added in the GItem box, and enter
   the command line in the ItemExe box. The item will now be in the group
   you specified.
<P>
</OL><h2>REFERENCES</h2>
 
<P>
For more information, please see the "Programmers Reference, Volume 1:
Overview Microsoft Windows SDK," chapter 17, "Shell Dynamic DataExchange
Interface." Also, look in the Windows SDK Help file in the Progman topic.
<P>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional reference words: 4.00 vb4win vb416<BR>
KBCategory: kbinterop kbprg kbcode kbhowto<BR>
KBSubcategory: IAPDDE<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  December 8, 1995</FONT>
	<BR>
	<A HREF="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
