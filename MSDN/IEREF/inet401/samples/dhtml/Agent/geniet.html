<HTML>
<HEAD>

<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title> Microsoft Agent Sample </TITLE>
<META HTTP-EQUIV="Content-Type" CONTENT="text/html; CHARSET=iso8859-1">
<META NAME="MS.LOCALE" CONTENT="EN-US">
<META NAME="ROBOTS" CONTENT="all">

</HEAD>
<BODY TOPMARGIN=0 LEFTMARGIN=20 BGCOLOR="#FFFFFF"  LINK="#000000" VLINK="#808080" ALINK="#000000">
<FONT FACE="VERDANA,ARIAL,HELVETICA" SIZE="2">

<TABLE CELLPADDING="0" CELLSPACING="0" BORDER="0" WIDTH="100%">
<TR>

<TD VALIGN=TOP ALIGN=LEFT><FONT FACE="VERDANA,ARIAL,HELVETICA" SIZE="6"><B>Microsoft Agent</B></FONT></TD><TD VALIGN=TOP ALIGN=RIGHT>
<A TARGET="_top" HREF="http://www.microsoft.com/">

<IMG SRC="ms_home.gif" BORDER="0" ALT="Microsoft">

</A>

</TD>

</TR>
</TABLE>


<P>
<BR>
Microsoft Agent provides access to a set of services that allow you to support an interactive character from your Web site.
<P>
Here is a sample demo using the Microsoft Agent ActiveX control. 
Using IE 4.0 events and object model, the character reads the text of the page
as the user moves his mouse. This sample degrades gracefully
under IE 3.0.
<BR>


<OBJECT ID="AgentX" width=0 height=0
 CLASSID="CLSID:F5BE8BD2-7DE6-11D0-91FE-00C04FD701A5"
 CODEBASE="#version=1,5,0,0"
 >
</OBJECT>

<OBJECT ID="TruVoice" width=0 height=0
 CLASSID="CLSID:B8F2846E-CE36-11D0-AC83-00C04FD97575"
 CODEBASE="#version=1,5,0,0"
 >
</OBJECT>

<SCRIPT LANGUAGE="VBScript">
<!--
'-------
'--Declare global variables
'-------

Dim Genie
Dim LoadRequest
Dim LoadShow
Dim LoadMove
Dim Speak1Request
Dim Speak2Request
Dim Speak3Request

'-------------------------------------------------
'-- window_OnLoad
'--
'-- This event gets fired when the page is loaded.
'--
'-------------------------------------------------
Sub window_OnLoad

	'----------
	'-- Load the character and initial animation data
	'----------
	AgentX.Connected = True	' temp patch for IE4
	Set LoadRequest = AgentX.Characters.Load("genie","http://agent.microsoft.com/characters/genie/genie.acf")
	Set Genie = AgentX.Characters("genie")

	Set LoadShow = Genie.Get ("state", "Showing, Speaking")

	'----------
	'-- And play intro animation
	'----------
	Genie.MoveTo 300,200
	
	Genie.Show

	DoIntro
		
End Sub

'-------------------------------------------------
'-- Button1_OnClick
'--
'-- This event is fired when someone clicks on the
'-- standard HTML button we have defined on the page.
'--
'-------------------------------------------------
Sub Button1_OnClick

	Genie.StopAll "Move, Play, Speak"	' button click stops character from speaking and flushes queue

	'----------
	'--If the character or initial animations aren't loaded yet
	'--then exit this subroutine
	'----------

	If LoadRequest.Status = 4 or LoadShow.Status = 4 Then
		Status = "Still loading...please wait to click the button." 
		Exit Sub

	ElseIf Speak1Request.Status = 4 or Speak1Request.Status = 2 Then
		Genie.Speak "Hey, I am not done speaking yet.|Please wait until I have finished talking.|Don't click that button yet.|You interrupted me.|Please let me finish my intro.|I'll just have to start over."
		DoIntro
		Exit Sub

	End If
	
	'----------
	'-- Check to see if the move animations are loaded yet 
	'----------
	
	If LoadMove.Status = 4 Then
		Genie.Speak "My moving animations are still loading."
		Genie.Speak "So I'll just slide around for now.|I'll just zip to the new location."
	
	End If

	'----------
	'-- Move the character 
	'----------
	

	Genie.MoveTo int(rnd*600), int(rnd*400)
	Set Speak2Request = Genie.Speak ("You clicked me from a web page button.|I love to fly.|This is fun!|Aren't I amazing?")

End Sub 

Sub AgentX_RequestStart(ByVal Request)

	'-------
	'--Change the status line in the browser when starting to load new data
	'-------

	If Request = LoadRequest Then
		Status = "One moment...loading the Genie character data"

	ElseIf Request = LoadShow Then
		Status = "One moment...loading the Showing animation data"

	ElseIf Request = LoadMove Then
		Status = "One moment...loading the Moving animation data"

	End If

End Sub

Sub AgentX_RequestComplete(ByVal Request)

	If Request = LoadRequest Then

		'--------
		'--If the character successfully loaded
		'--------
		
		If Request.Status = 0 Then
			Status = "Genie character data loaded."
		
		'-------
		'--If the character load failed
		'-------

		ElseIf Request.Status = 1 Then
			msgbox "Unable to download the character data."& chr(13) & "Try refreshing this page"
		
		End If

	ElseIf Request = LoadShow Then

		'--------
		'--If the showing animation successfully loaded
		'--------

		If Request.Status = 0 Then
			Status = "Showing animation data loaded."

			'Now load the moving animations
			Set LoadMove = Genie.Get ("state", "moving", False)
		
		'-------
		'--If the showing animation load failed
		'-------

		ElseIf Request.Status = 1 Then
			msgbox "Unable to download animation. Try refreshing the page"
		
		End If

	ElseIf Request = LoadMove Then

		'--------
		'--If the moving animations successfully loaded
		'--------

		If Request.Status = 0 Then
			Status = "Moving state animation data loaded."
		
		'-------
		'--If the moving animations load failed
		'-------

		ElseIf Request.Status = 1 Then
			Genie.Stop Speak1Request
			Genie.Speak "Uh oh, my moving animations failed to load. Try refreshing this page."
		
		End If

	End If

End Sub

Sub DoIntro

	SpeakText1 = "Hello, I am Genie, your software agent. "
	SpeakText2 = "This is the Microsoft Agent Sample Page. "
	SpeakText3 = "I can move around and talk and react to events generated from user input to your page. "
	SpeakText4 = "Click the Try It button to try me out."
	
	Set Speak1Request = Genie.Speak (SpeakText1+SpeakText2+SpeakText3+SpeakText4)

End Sub
Sub document_onmouseover()
	on error resume next
	szname = window.event.srcElement		' get the object that fired the event
	szname1 = lcase(window.event.srcElement.tagname) ' Which tag is the mouse over on the page?
	if szname1 <> "body"  then 			' do it for the childern of body only
		window.event.srcElement.style.color="red"	' turn its color to red

							' Agent spells uppercase words
		sztext = "It reads, " + lcase(window.event.srcElement.innerText )' and what does the tag say?
		AgentS ="You are pointing to,  a " + szname1
		Set Speak3Request = Genie.Speak (AgentS)
		Set Speak3Request = Genie.Speak (sztext)
	end if
end sub


Sub document_onmouseout()
	on error resume next
	szname = lcase(window.event.srcElement)
	if szname <> "body"  then 
		window.event.srcElement.style.color="black"	' set the color back to black
		
	end if
end sub
-->

</SCRIPT>

<FORM>
<CENTER><INPUT TYPE=BUTTON ID=BUTTON1 VALUE="Try It"></CENTER>
</FORM>

</P>
<BR><BR>For additional samples and complete documentation, or to download Microsoft Agent, visit either of these sites:

<UL>
<LI><B><A HREF="http://www.microsoft.com/workshop/prog/agent/" target="_top">The Microsoft Agent Web site</A></B>
<LI><B><A HREF="http:www.microsoft.com.msdn/sdk/inetsdk/asetup/default.asp" target="_top">The Internet Client SDK Download area</A></B>
</UL>

<P><P><FONT FACE="MS SANS SERIF" SIZE="1" COLOR="BLACK">
<A CLASS=cpslug HREF="http://www.microsoft.com/misc/cpyright.htm" TARGET="_top">&#169; 1997 Microsoft Corporation. All rights reserved. Terms of Use.</A>
</FONT>
<BR CLEAR=ALL><P>
</FONT></BODY>
</HTML>

