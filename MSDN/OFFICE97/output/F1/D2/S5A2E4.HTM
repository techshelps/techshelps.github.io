<HTML>
<HEAD>

<meta http-equiv="Content-Type" content="text/html; charset=iso8859-1">

<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>RecordsAffected Property</TITLE><style>@import url(msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="msdn_ie3.css"></HEAD><BODY BGCOLOR="#FFFFFF">

<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

<FONT FACE="VERDANA,ARIAL,HELVETICA" SIZE=2>
	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->



<H1>RecordsAffected Property</H1>Applies To</P>
<SPAN CLASS="eln">Connection</SPAN> object, <SPAN CLASS="eln">Database</SPAN> object, <SPAN CLASS="eln">QueryDef</SPAN> object.</P>
Description</P>
Returns the number of records affected by the most recently invoked Execute method.</P>
Return Values</P>
The return value is a <SPAN CLASS="eln">Long</SPAN> from 0 to the number of records affected by the most recently invoked <SPAN CLASS="eln">Execute</SPAN> method on either a Database or QueryDef object.</P>
Remarks</P>
When you use the <SPAN CLASS="eln">Execute</SPAN> method to run an <SPAN CLASS="pop">action query</SPAN> from a <SPAN CLASS="eln">QueryDef</SPAN> object, the <SPAN CLASS="eln">RecordsAffected</SPAN> property will contain the number of records deleted, updated, or&nbsp;inserted.</P>
When you use <B>RecordsAffected</B> in an <SPAN CLASS="pop">ODBCDirect workspace</SPAN>, it will not return a useful value from an SQL DROP TABLE action query.</P>
Example</P>
This example uses the <SPAN CLASS="eln">RecordsAffected</SPAN> property with action queries executed from a <SPAN CLASS="eln">Database</SPAN> object and from a <SPAN CLASS="eln">QueryDef</SPAN> object. The RecordsAffectedOutput function is required for this procedure to run.</P>

<pre><code>Sub RecordsAffectedX()

    Dim dbsNorthwind As Database
    Dim qdfTemp As QueryDef
    Dim strSQLChange As String
    Dim strSQLRestore As String

    Set dbsNorthwind = OpenDatabase("Northwind.mdb")

    With dbsNorthwind
        ' Print report of contents of the Employees
        ' table.
        Debug.Print _
            "Number of records in Employees table: " &amp; _
            .TableDefs!Employees.RecordCount
        RecordsAffectedOutput dbsNorthwind

        ' Define and execute an action query.
        strSQLChange = "UPDATE Employees " &amp; _
            "SET Country = 'United States' " &amp; _
            "WHERE Country = 'USA'"
        .Execute strSQLChange

        ' Print report of contents of the Employees
        ' table.
        Debug.Print _
            "RecordsAffected after executing query " &amp; _
            "from Database: " &amp; .RecordsAffected
        RecordsAffectedOutput dbsNorthwind

        ' Define and run another action query.
        strSQLRestore = "UPDATE Employees " &amp; _
            "SET Country = 'USA' " &amp; _
            "WHERE Country = 'United States'"
        Set qdfTemp = .CreateQueryDef("", strSQLRestore)
        qdfTemp.Execute

        ' Print report of contents of the Employees
        ' table.
        Debug.Print _
            "RecordsAffected after executing query " &amp; _
            "from QueryDef: " &amp; qdfTemp.RecordsAffected
        RecordsAffectedOutput dbsNorthwind

        .Close

    End With

End Sub

Function RecordsAffectedOutput(dbsNorthwind As Database)

    Dim rstEmployees As Recordset

    ' Open a Recordset object from the Employees table.
    Set rstEmployees = _
        dbsNorthwind.OpenRecordset("Employees")

    With rstEmployees
        ' Enumerate Recordset.
        .MoveFirst
        Do While Not .EOF
            Debug.Print "    " &amp; !LastName &amp; ", " &amp; !Country
            .MoveNext
        Loop
        .Close
    End With

End Function</code></pre>
Example (Microsoft&nbsp;Access)</P>
The following example prints the number of records updated by an action query:</P>

<pre><code>Sub RecordsUpdated()
    Dim dbs As Database, qdf As QueryDef
    Dim strSQL As String

    ' Return reference to current database.
    Set dbs = CurrentDb
    strSQL = "UPDATE Employees SET Title = " _
        &amp; "'Senior Sales Representative' " &amp; "WHERE Title = 'Sales Representative';"
    ' Create new QueryDef object.
    Set qdf = dbs.CreateQueryDef("UpdateTitles", strSQL)
    ' Execute QueryDef object.
    qdf.Execute
    Debug.Print qdf.RecordsAffected
    Set dbs = Nothing
End Sub</code></pre>
<HR CLASS="rr"></FONT></BODY>
</HTML>
