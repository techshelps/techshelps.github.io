<HTML>
<HEAD>

<meta http-equiv="Content-Type" content="text/html; charset=iso8859-1">

<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Checked Property</TITLE><style>@import url(msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="msdn_ie3.css"></HEAD><BODY BGCOLOR="#FFFFFF">

<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

<FONT FACE="VERDANA,ARIAL,HELVETICA" SIZE=2>
	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->



<H1>Checked Property</H1>Applies To</P>
<SPAN CLASS="eln">BalloonCheckbox</SPAN><SPAN CLASS="lee"> object.</SPAN></P>
<SPAN CLASS="lee">Description</SPAN></P>
<SPAN CLASS="lee">True</SPAN> if the specified check box in the Office&nbsp;Assistant balloon is selected. Read/write <SPAN CLASS="lee">Boolean</SPAN>.</P>
See Also</P>
<SPAN CLASS="eln">Balloon</SPAN> object, <SPAN CLASS="eln">BalloonCheckboxes</SPAN> collection object.</P>
Example</P>
This example creates a balloon with a heading, text, and three region choices. When the user selects a check box and then clicks <SPAN CLASS="eln">OK</SPAN> in the balloon, the appropriate procedure is run.</P>

<pre><code>With Assistant.NewBalloon
    .Heading = "Regional Sales Data"
    .Text = "Select your region"
    For i = 1 To 3
        .CheckBoxes(i).Text = "Region " &amp; i
    Next
    .Button = msoButtonSetOkCancel
    .Show
Select Case True
    Case .CheckBoxes(1).Checked
        runregion1
    Case .CheckBoxes(2).Checked
        runregion2
    Case .CheckBoxes(3).Checked
        runregion3
End Select
End With</code></pre>
Example (Microsoft Access)</P>
The following function creates a custom balloon for the Office&nbsp;Assistant and determines how the user responded to the balloon:</P>

<pre><code>Function AssistantBalloon(Optional varCheck As Variant, Optional _
    varLabel As Variant)
    Dim bch As BalloonCheckbox
    Dim intI As Integer
    Dim intReturn As Integer
    Dim strCheck(5) As String
    Dim strList As String

    ' Create new balloon.
    Set bal = Assistant.NewBalloon
    ' Specify balloon type.
    bal.BalloonType = msoBalloonTypeButtons
    ' Specify that balloon is modal.
    bal.Mode = msoModeModal

    ' Make Assistant visible.
    If Assistant.Visible = False Then Assistant.Visible = True
    ' Check if first argument has been passed.
    If Not IsMissing(varCheck) Then
        ' If too large, set to maximum number of check boxes (5).
        If varCheck &gt; 6 Then
            varCheck = 5
        End If
        ' Set text property to alphabet character.
        For intI = 1 To varCheck
            bal.Checkboxes(intI).Text = Chr(64 + intI)
        Next intI
    End If

    If Not IsMissing(varLabel) Then
        ' If too large, set to maximum number of labels (5).
        If varLabel &gt; 6 Then
            varLabel = 5
        End If
        For intI = 1 To varLabel
            ' Set text property to alphabet character.
            bal.Labels(intI).Text = Chr(64 + intI)
        Next intI
    End If
    ' Store return value.
    intReturn = bal.Show

    intI = 0
    ' Determine which check boxes were checked, if any.
    For Each bch In bal.Checkboxes
        If bch.Checked = True Then
            strCheck(intI) = bch.Text
            strList = strList &amp; "'" &amp; strCheck(intI) &amp; "'" &amp; Chr(32)
        End If
        intI = intI + 1
    Next
    If Len(strList) &lt;&gt; 0 Then
        MsgBox "You selected checkboxes " &amp; strList &amp; "."
    End If
    ' Determine which label was selected, if any.
    If intReturn &gt; 0 Then
        MsgBox "You selected label " &amp; bal.Labels(intReturn).Text &amp; "."
    End If
End Function</code></pre>
You could call this function from the Debug window as follows:</P>

<pre><code>? AssistantBalloon(4, 5)</code></pre>
<HR CLASS="rr"></FONT></BODY>
</HTML>
