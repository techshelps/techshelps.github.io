<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>TRANSFORM Statement Example (DAO)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="dasqltransformx"></a>TRANSFORM Statement Example</b></p>
<p>
This example uses the SQL TRANSFORM clause to create a <a href="daglos01_8veh.htm">crosstab query</a> showing the number of orders taken by each employee for each calendar quarter of 1994. The SQLTRANSFORMOutput function is required for this procedure to run.</p>
<pre><code>Sub TransformX1()

   Dim dbs As Database
   Dim strSQL As String
   Dim qdfTRANSFORM As QueryDef

   strSQL = "PARAMETERS prmYear SHORT; TRANSFORM " _
      &amp; "Count(OrderID) " _
      &amp; "SELECT FirstName &amp; "" "" &amp; LastName AS " _
      &amp; "FullName FROM Employees INNER JOIN Orders " _
      &amp; "ON Employees.EmployeeID = " _
      &amp; "Orders.EmployeeID WHERE DatePart " _
      &amp; "(""yyyy"", OrderDate) = [prmYear] "
 
    strSQL = strSQL &amp; "GROUP BY FirstName &amp; " _
      &amp; """ "" &amp; LastName " _
      &amp; "ORDER BY FirstName &amp; "" "" &amp; LastName " _
      &amp; "PIVOT DatePart(""q"", OrderDate)"
   
   ' Modify this line to include the path to Northwind
   ' on your computer.
   Set dbs = OpenDatabase("Northwind.mdb")

   Set qdfTRANSFORM = dbs.CreateQueryDef _
      ("", strSQL)
   
   SQLTRANSFORMOutput qdfTRANSFORM, 1994
   
   dbs.Close

End Sub
</code></pre>
<p>
This example uses the SQL TRANSFORM clause to create a slightly more complex crosstab query showing the total dollar amount of orders taken by each employee for each calendar quarter of 1994. The SQLTRANSFORMOutput function is required for this procedure to run.</p>
<pre><code>Sub TransformX2()

   Dim dbs As Database
   Dim strSQL As String
   Dim qdfTRANSFORM As QueryDef

   strSQL = "PARAMETERS prmYear SHORT; TRANSFORM " _
      &amp; "Sum(Subtotal) SELECT FirstName &amp; "" """ _
      &amp; "&amp; LastName AS FullName " _
      &amp; "FROM Employees INNER JOIN " _
      &amp; "(Orders INNER JOIN [Order Subtotals] " _
      &amp; "ON Orders.OrderID = " _
      &amp; "[Order Subtotals].OrderID) " _
      &amp; "ON Employees.EmployeeID = " _
      &amp; "Orders.EmployeeID WHERE DatePart" _
      &amp; "(""yyyy"", OrderDate) = [prmYear] "
 
    strSQL = strSQL &amp; "GROUP BY FirstName &amp; "" """ _
      &amp; "&amp; LastName " _
      &amp; "ORDER BY FirstName &amp; "" "" &amp; LastName " _
      &amp; "PIVOT DatePart(""q"",OrderDate)"      
      
   ' Modify this line to include the path to Northwind
   ' on your computer.
   Set dbs = OpenDatabase("Northwind.mdb")

   Set qdfTRANSFORM = dbs.CreateQueryDef _
      ("", strSQL)
   
   SQLTRANSFORMOutput qdfTRANSFORM, 1994
   
   dbs.Close

End Sub

Function SQLTRANSFORMOutput(qdfTemp As QueryDef, _
   intYear As Integer)
   
   Dim rstTRANSFORM As Recordset
   Dim fldLoop As Field
   Dim booFirst As Boolean

   qdfTemp.PARAMETERS!prmYear = intYear
   Set rstTRANSFORM = qdfTemp.OpenRecordset()
   
   Debug.Print qdfTemp.SQL
   Debug.Print
   Debug.Print , , "Quarter"

   With rstTRANSFORM
      booFirst = True
      For Each fldLoop In .Fields
         If booFirst = True Then
            Debug.Print fldLoop.Name
            Debug.Print , ;
            booFirst = False
         Else
            Debug.Print , fldLoop.Name;
         End If
      Next fldLoop
      Debug.Print
      
      Do While Not .EOF
         booFirst = True
         For Each fldLoop In .Fields
            If booFirst = True Then
               Debug.Print fldLoop
               Debug.Print , ;
               booFirst = False
            Else
               Debug.Print , fldLoop;
            End If
         Next fldLoop
         Debug.Print
         .MoveNext
      Loop
   End With
   
End Function
</code></pre>
</font></BODY>
</HTML>
