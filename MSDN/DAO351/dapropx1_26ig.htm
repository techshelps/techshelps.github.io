<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>OrdinalPosition Property Example (DAO)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="daproordinalpositionx"></a>OrdinalPosition Property Example</b></p>
<p>
This example changes the <b>OrdinalPosition</b> property values in the Employees <b>TableDef</b> in order to control the <b>Field</b> order in a resulting <b>Recordset</b>. By setting the <b>OrdinalPosition</b> of all the <b>Fields</b> to 1, any resulting <b>Recordset</b> will order the <b>Fields</b> alphabetically. Note that the <b>OrdinalPosition</b> values in the <b>Recordset</b> don't match the values in the <b>TableDef</b>, but simply reflect the end result of the <b>TableDef</b> changes.</p>
<pre><code>Sub OrdinalPositionX()

   Dim dbsNorthwind As Database
   Dim tdfEmployees As TableDef
   Dim aintPosition() As Integer
   Dim astrFieldName() As String
   Dim intTemp As Integer
   Dim fldTemp As Field
   Dim rstEmployees As Recordset

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set tdfEmployees = dbsNorthwind.TableDefs("Employees")

   With tdfEmployees
      ' Display and store original OrdinalPosition data.
      Debug.Print _
         "Original OrdinalPosition data in TableDef."
      ReDim aintPosition(0 To .Fields.Count - 1) As Integer
      ReDim astrFieldName(0 To .Fields.Count - 1) As String
      For intTemp = 0 To .Fields.Count - 1
         aintPosition(intTemp) = _
            .Fields(intTemp).OrdinalPosition
         astrFieldName(intTemp) = .Fields(intTemp).Name
         Debug.Print , aintPosition(intTemp), _
            astrFieldName(intTemp)
      Next intTemp

      ' Change OrdinalPosition data.
      For Each fldTemp In .Fields
         fldTemp.OrdinalPosition = 1
      Next fldTemp

      ' Open new Recordset object to show how the 
      ' OrdinalPosition data has affected the record order.
      Debug.Print _
         "OrdinalPosition data from resulting Recordset."
      Set rstEmployees = dbsNorthwind.OpenRecordset( _
         "SELECT * FROM Employees")
      For Each fldTemp In rstEmployees.Fields
         Debug.Print , fldTemp.OrdinalPosition, fldTemp.Name
      Next fldTemp
      rstEmployees.Close

      ' Restore original OrdinalPosition data because this is
      ' a demonstration.
      For intTemp = 0 To .Fields.Count - 1
         .Fields(astrFieldName(intTemp)).OrdinalPosition = _
            aintPosition(intTemp)
      Next intTemp

   End With

   dbsNorthwind.Close

End Sub</code></pre>
</font></BODY>
</HTML>
