<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>AllowZeroLength Property Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="daproallowzerolengthx"></a>AllowZeroLength Property Example</b></p>
<p>
In this example, the <b>AllowZeroLength</b> property allows the user to set the value of a <b>Field</b> to an empty string. In this situation, the user can distinguish between a record where data is not known and a record where the data does not apply.</p>
<pre><code>Sub AllowZeroLengthX()

   Dim dbsNorthwind As Database
   Dim tdfEmployees As TableDef
   Dim fldTemp As Field
   Dim rstEmployees As Recordset
   Dim strMessage As String
   Dim strInput As String

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set tdfEmployees = dbsNorthwind.TableDefs("Employees")
   ' Create a new Field object and append it to the Fields 
   ' collection of the Employees table.
   Set fldTemp = tdfEmployees.CreateField("FaxPhone", _
      dbText, 24)
   fldTemp.AllowZeroLength = True
   tdfEmployees.Fields.Append fldTemp

   Set rstEmployees = _
      dbsNorthwind.OpenRecordset("Employees")

   With rstEmployees
      ' Get user input.
      .Edit
      strMessage = "Enter fax number for " &amp; _
         !FirstName &amp; " " &amp; !LastName &amp; "." &amp; vbCr &amp; _
         "[? - unknown, X - has no fax]"
      strInput = UCase(InputBox(strMessage))
      If strInput &lt;&gt; "" Then
         Select Case strInput
            Case "?"
               !FaxPhone = Null
            Case "X"
               !FaxPhone = ""
            Case Else
               !FaxPhone = strInput
         End Select

         .Update

         ' Print report.
         Debug.Print "Name - Fax number"
         Debug.Print !FirstName &amp; " " &amp; !LastName &amp; " - ";

         If IsNull(!FaxPhone) Then
            Debug.Print "[Unknown]"
         Else
            If !FaxPhone = "" Then
               Debug.Print "[Has no fax]"
            Else
               Debug.Print !FaxPhone
            End If
         End If

      Else
         .CancelUpdate
      End If

      .Close
   End With

   ' Delete new field because this is a demonstration.
   tdfEmployees.Fields.Delete fldTemp.Name
   dbsNorthwind.Close

End Sub</code></pre>
</font></BODY>
</HTML>
