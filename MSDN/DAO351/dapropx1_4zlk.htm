<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>IgnoreNulls Property Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="daproignorenullsx"></a>IgnoreNulls Property Example</b></p>
<p>
This example sets the <b>IgnoreNulls</b> property of a new <b>Index</b> to <b>True</b> or <b>False</b> based on user input, and then demonstrates the effect on a <b>Recordset</b> with a record whose key field contains a <b>Null</b> value.</p>
<pre><code>Sub IgnoreNullsX()

   Dim dbsNorthwind As Database
   Dim tdfEmployees As TableDef
   Dim idxNew As Index
   Dim rstEmployees As Recordset

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set tdfEmployees = dbsNorthwind!Employees

   With tdfEmployees
      ' Create a new Index object.
      Set idxNew = .CreateIndex("NewIndex")
      idxNew.Fields.Append idxNew.CreateField("Country")

      ' Set the IgnoreNulls property of the new Index object 
      ' based on the user's input.
      Select Case MsgBox("Set IgnoreNulls to True?", _
         vbYesNoCancel)
         Case vbYes
            idxNew.IgnoreNulls = True
         Case vbNo
            idxNew.IgnoreNulls = False
         Case Else
            dbsNorthwind.Close
            End
      End Select

      ' Append the new Index object to the Indexes 
      ' collection of the Employees table.
      .Indexes.Append idxNew
      .Indexes.Refresh
   End With

   Set rstEmployees = _
      dbsNorthwind.OpenRecordset("Employees")

   With rstEmployees
      ' Add a new record to the Employees table.
      .AddNew
      !FirstName = "Gary"
      !LastName = "Haarsager"
      .Update

      ' Use the new index to set the order of the records.
      .Index = idxNew.Name
      .MoveFirst

      Debug.Print "Index = " &amp; .Index &amp; _
         ", IgnoreNulls = " &amp; idxNew.IgnoreNulls
      Debug.Print "&nbsp; Country - Name"

      ' Enumerate the Recordset. The value of the 
      ' IgnoreNulls property will determine if the newly 
      ' added record appears in the output.
      Do While Not .EOF
         Debug.Print "&nbsp;&nbsp;&nbsp; " &amp; _
            IIf(IsNull(!Country), "[Null]", !Country) &amp; _
            " - " &amp; !FirstName &amp; " " &amp; !LastName
         .MoveNext
      Loop

      ' Delete new record because this is a demonstration.
      .Index = ""
      .Bookmark = .LastModified
      .Delete
      .Close
   End With

   ' Delete new Index because this is a demonstration.
   tdfEmployees.Indexes.Delete idxNew.Name
   dbsNorthwind.Close

End Sub</code></pre>
</font></BODY>
</HTML>
