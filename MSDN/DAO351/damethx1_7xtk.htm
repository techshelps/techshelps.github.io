<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Refresh Method Example (DAO)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthrefreshx"></a>Refresh Method Example</b></p>
<p>
This example uses the <b>Refresh</b> method to update the <b>Fields</b> collection of the Categories table based on changes to the <b>OrdinalPosition</b> data. The order of the <b>Fields</b> in the collection changes only after the <b>Refresh</b> method is used.</p>
<pre><code>Sub RefreshX()

   Dim dbsNorthwind As Database
   Dim tdfEmployees As TableDef
   Dim aintPosition() As Integer
   Dim astrFieldName() As String
   Dim intTemp As Integer
   Dim fldLoop As Field

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set tdfEmployees = dbsNorthwind.TableDefs("Categories")

   With tdfEmployees
      ' Display original OrdinalPosition data and store it
      ' in an array.
      Debug.Print _
         "Original OrdinalPosition data in TableDef."
      ReDim aintPosition(0 To .Fields.Count - 1) As Integer
      ReDim astrFieldName(0 To .Fields.Count - 1) As String
      For intTemp = 0 To .Fields.Count - 1
         aintPosition(intTemp) = _
            .Fields(intTemp).OrdinalPosition
         astrFieldName(intTemp) = .Fields(intTemp).Name
         Debug.Print , aintPosition(intTemp), _
            astrFieldName(intTemp)
      Next intTemp

      ' Change OrdinalPosition data.
      For Each fldLoop In .Fields
         fldLoop.OrdinalPosition = _
            100 - fldLoop.OrdinalPosition
      Next fldLoop
      Set fldLoop = Nothing

      ' Print new data.
      Debug.Print "New OrdinalPosition data before Refresh."
      For Each fldLoop In .Fields
         Debug.Print , fldLoop.OrdinalPosition, fldLoop.Name
      Next fldLoop

      .Fields.Refresh

      ' Print new data, showing how the field order has been 
      ' changed.
      Debug.Print "New OrdinalPosition data after Refresh."
      For Each fldLoop In .Fields
         Debug.Print , fldLoop.OrdinalPosition, fldLoop.Name
      Next fldLoop

      ' Restore original OrdinalPosition data.
      For intTemp = 0 To .Fields.Count - 1
         .Fields(astrFieldName(intTemp)).OrdinalPosition = _
            aintPosition(intTemp)
      Next intTemp
   End With

   dbsNorthwind.Close

End Sub</code></pre>
</font></BODY>
</HTML>
