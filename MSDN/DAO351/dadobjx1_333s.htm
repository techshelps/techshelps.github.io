<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Group Object, Groups Collection, User Object, and Users Collection Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="daobjgroupx"></a>Group Object, Groups Collection, User Object, and Users Collection Example</b></p>
<p>
This example illustrates the use of the <b>Group</b> and <b>User</b> objects and the <b>Groups</b> and <b>Users</b> collections. First, it creates a new <b>User</b> object and appends the object to the <b>Users</b> collection of the default <b>Workspace</b> object. Next, it creates a new <b>Group</b> object and appends the object to the <b>Groups</b> collection of the default <b>Workspace</b> object. Then the example adds user Pat Smith to the Accounting group. Finally, it enumerates the <b>Users</b> and <b>Groups</b> collections of the default <b>Workspace</b> object. See the methods and properties listed in the <b>Group</b> and <b>User</b> summary topics for additional examples.</p>
<pre><code>Sub GroupX()

   Dim wrkDefault As Workspace
   Dim usrNew As User
   Dim usrLoop As User
   Dim grpNew As Group
   Dim grpLoop As Group
   Dim grpMember As Group

   Set wrkDefault = DBEngine.Workspaces(0)

   With wrkDefault

      ' Create and append new user.
      Set usrNew = .CreateUser("Pat Smith", _
         "abc123DEF456", "Password1")
      .Users.Append usrNew

      ' Create and append new group.
      Set grpNew = .CreateGroup("Accounting", _
         "UVW987xyz654")
      .Groups.Append grpNew

      ' Make the user Pat Smith a member of the 
      ' Accounting group by creating and adding the 
      ' appropriate Group object to the user's Groups 
      ' collection. The same is accomplished if a User 
      ' object representing Pat Smith is created and 
      ' appended to the Accounting group's Users 
      ' collection.
      Set grpMember = usrNew.CreateGroup("Accounting")
      usrNew.Groups.Append grpMember

      Debug.Print "Users collection:"

      ' Enumerate all User objects in the default
      ' workspace's Users collection.
      For Each usrLoop In .Users
         Debug.Print "&nbsp; " &amp; usrLoop.Name
         Debug.Print "&nbsp;&nbsp;&nbsp; Belongs to these groups:"

         ' Enumerate all Group objects in each User
         ' object's Groups collection.
         If usrLoop.Groups.Count &lt;&gt; 0 Then
            For Each grpLoop In usrLoop.Groups
               Debug.Print "&nbsp;&nbsp;&nbsp; " &amp; _
                  grpLoop.Name
            Next grpLoop
         Else
            Debug.Print "&nbsp;&nbsp;&nbsp; [None]"
         End If

      Next usrLoop

      Debug.Print "Groups collection:"

      ' Enumerate all Group objects in the default
      ' workspace's Groups collection.
      For Each grpLoop In .Groups
         Debug.Print "&nbsp; " &amp; grpLoop.Name
         Debug.Print "&nbsp;&nbsp;&nbsp; Has as its members:"

         ' Enumerate all User objects in each Group
         ' object's Users collection.
         If grpLoop.Users.Count &lt;&gt; 0 Then
            For Each usrLoop In grpLoop.Users
               Debug.Print "&nbsp;&nbsp;&nbsp; " &amp; _
                  usrLoop.Name
            Next usrLoop
         Else
            Debug.Print "&nbsp;&nbsp;&nbsp; [None]"
         End If

      Next grpLoop

      ' Delete new User and Group objects because this 
      ' is only a demonstration.
      .Users.Delete "Pat Smith"
      .Groups.Delete "Accounting"

   End With

End Sub</code></pre>
</font></BODY>
</HTML>
