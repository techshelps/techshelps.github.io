<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>TableDef Object, TableDefs Collection Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="daobjtabledefx"></a>TableDef Object, TableDefs Collection Example</b></p>
<p>
This example creates a new <b>TableDef</b> object and appends it to the <b>TableDefs</b> collection of the Northwind <b>Database</b> object. It then enumerates the <b>TableDefs</b> collection and the <b>Properties</b> collection of the new <b>TableDef</b>.</p>
<pre><code>Sub TableDefX()

   Dim dbsNorthwind As Database
   Dim tdfNew As TableDef
   Dim tdfLoop As TableDef
   Dim prpLoop As Property

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")

   ' Create new TableDef object, append Field objects 
   ' to its Fields collection, and append TableDef 
   ' object to the TableDefs collection of the 
   ' Database object.
   Set tdfNew = dbsNorthwind.CreateTableDef("NewTableDef")
   tdfNew.Fields.Append tdfNew.CreateField("Date", dbDate)
   dbsNorthwind.TableDefs.Append tdfNew

   With dbsNorthwind
      Debug.Print .TableDefs.Count &amp; _
         " TableDefs in " &amp; .Name

      ' Enumerate TableDefs collection.
      For Each tdfLoop In .TableDefs
         Debug.Print "&nbsp; " &amp; tdfLoop.Name
      Next tdfLoop

      With tdfNew
         Debug.Print "Properties of " &amp; .Name

         ' Enumerate Properties collection of new
         ' TableDef object, only printing properties
         ' with non-empty values.
         For Each prpLoop In .Properties
            Debug.Print "&nbsp; " &amp; prpLoop.Name &amp; " - " &amp; _
               IIf(prpLoop = "", "[empty]", prpLoop)
         Next prpLoop

      End With

      ' Delete new TableDef since this is a 
      ' demonstration.
      .TableDefs.Delete tdfNew.Name
      .Close
   End With

End Sub</code></pre>
</font></BODY>
</HTML>
