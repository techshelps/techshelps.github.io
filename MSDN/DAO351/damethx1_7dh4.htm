<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>FindFirst, FindLast, FindNext, FindPrevious Methods Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthfindfirstx"></a>FindFirst, FindLast, FindNext, FindPrevious Methods Example</b></p>
<p>
This example uses the <b>FindFirst</b>, <b>FindLast</b>, <b>FindNext</b>, and <b>FindPrevious</b> methods to move the record pointer of a <b>Recordset</b> based on the supplied search string and command. The FindAny function is required for this procedure to run.</p>
<pre><code>Sub FindFirstX()

   Dim dbsNorthwind As Database
   Dim rstCustomers As Recordset
   Dim strCountry As String
   Dim varBookmark As Variant
   Dim strMessage As String
   Dim intCommand As Integer

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set rstCustomers = dbsNorthwind.OpenRecordset( _
      "SELECT CompanyName, City, Country " &amp; _
      "FROM Customers ORDER BY CompanyName", _
      dbOpenSnapshot)

   Do While True
      ' Get user input and build search string.
      strCountry = _
         Trim(InputBox("Enter country for search."))
      If strCountry = "" Then Exit Do
      strCountry = "Country = '" &amp; strCountry &amp; "'"

      With rstCustomers
         ' Populate recordset.
         .MoveLast
         ' Find first record satisfying search string. Exit 
         ' loop if no such record exists.
         .<i>FindFirst</i> strCountry
         If .NoMatch Then
            MsgBox "No records found with " &amp; _
               strCountry &amp; "."
            Exit Do
         End If

         Do While True
            ' Store bookmark of current record.
            varBookmark = .Bookmark
            ' Get user choice of which method to use.
            strMessage = "Company: " &amp; !CompanyName &amp; _
               vbCr &amp; "Location: " &amp; !City &amp; ", " &amp; _
               !Country &amp; vbCr &amp; vbCr &amp; _
               strCountry &amp; vbCr &amp; vbCr &amp; _
               "[1 - FindFirst, 2 - FindLast, " &amp; _
               vbCr &amp; "3 - FindNext, " &amp; _
               "4 - FindPrevious]"
            intCommand = Val(Left(InputBox(strMessage), 1))
            If intCommand &lt; 1 Or intCommand &gt; 4 Then Exit Do

            ' Use selected Find method. If the Find fails, 
            ' return to the last current record.
            If FindAny(intCommand, rstCustomers, _
                  strCountry) = False Then
               .Bookmark = varBookmark
               MsgBox "No match--returning to " &amp; _
                  "current record."
            End If

         Loop

      End With

      Exit Do
   Loop

   rstCustomers.Close
   dbsNorthwind.Close

End Sub

Function FindAny(intChoice As Integer, _
   rstTemp As Recordset, _
   strFind As String) As Boolean

   ' Use Find method based on user input.
   Select Case intChoice
      Case 1
         rstTemp.<i>FindFirst</i> strFind
      Case 2
         rstTemp.<i>FindLast</i> strFind
      Case 3
         rstTemp.<i>FindNext</i> strFind
      Case 4
         rstTemp.<i>FindPrevious</i> strFind
   End Select

   ' Set return value based on NoMatch property.
   FindAny = IIf(rstTemp.NoMatch, False, True)

End Function</code></pre>
</font></BODY>
</HTML>
