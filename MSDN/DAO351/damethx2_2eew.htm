<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>CopyQueryDef Method Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthcopyquerydefx"></a>CopyQueryDef Method Example</b></p>
<p>
This example uses the <b>CopyQueryDef</b> method to create a copy of a <b>QueryDef</b> from an existing <b>Recordset</b> and modifies the copy by adding a clause to the <b>SQL</b> property. When you create a permanent <b>QueryDef, </b>spaces, semicolons, or linefeeds may be added to the <b>SQL</b> property; these extra characters must be stripped before any new clauses can be attached to the SQL statement.</p>
<pre><code>Function CopyQueryNew(rstTemp As Recordset, _
   strAdd As String) As QueryDef

   Dim strSQL As String
   Dim strRightSQL As String

   Set CopyQueryNew = rstTemp.<span class=cfe><b>CopyQueryDef</b></span>
   With CopyQueryNew
      ' Strip extra characters.
      strSQL = .SQL
      strRightSQL = Right(strSQL, 1)
      Do While strRightSQL = " " Or strRightSQL = ";" Or _
            strRightSQL = Chr(10) Or strRightSQL = vbCr
         strSQL = Left(strSQL, Len(strSQL) - 1)
         strRightSQL = Right(strSQL, 1)
      Loop
      .SQL = strSQL &amp; strAdd
   End With

End Function
</code></pre>
<p>
This example shows a possible use of CopyQueryNew().</p>
<pre><code>Sub CopyQueryDefX()

   Dim dbsNorthwind As Database
   Dim qdfEmployees As QueryDef
   Dim rstEmployees As Recordset
   Dim intCommand As Integer
   Dim strOrderBy As String
   Dim qdfCopy As QueryDef
   Dim rstCopy As Recordset

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set qdfEmployees = dbsNorthwind.CreateQueryDef( _
      "NewQueryDef", "SELECT FirstName, LastName, " &amp; _
      "BirthDate FROM Employees")
   Set rstEmployees = qdfEmployees.OpenRecordset( _
      dbOpenForwardOnly)

   Do While True
      intCommand = Val(InputBox( _
         "Choose field on which to order a new " &amp; _
         "Recordset:" &amp; vbCr &amp; "1 - FirstName" &amp; vbCr &amp; _
         "2 - LastName" &amp; vbCr &amp; "3 - BirthDate" &amp; vbCr &amp; _
         "[Cancel - exit]"))
      Select Case intCommand
         Case 1
            strOrderBy = " ORDER BY FirstName"
         Case 2
            strOrderBy = " ORDER BY LastName"
         Case 3
            strOrderBy = " ORDER BY BirthDate"
         Case Else
            Exit Do
      End Select
      Set qdfCopy = CopyQueryNew(rstEmployees, strOrderBy)
      Set rstCopy = qdfCopy.OpenRecordset(dbOpenSnapshot, _
         dbForwardOnly)
      With rstCopy
         Do While Not .EOF
            Debug.Print !LastName &amp; ", " &amp; !FirstName &amp; _
               " - " &amp; !BirthDate
            .MoveNext
         Loop
         .Close
      End With
      Exit Do
   Loop

   rstEmployees.Close
   ' Delete new QueryDef because this is a demonstration.
   dbsNorthwind.QueryDefs.Delete qdfEmployees.Name
   dbsNorthwind.Close

End Sub
</code></pre>
</font></BODY>
</HTML>
