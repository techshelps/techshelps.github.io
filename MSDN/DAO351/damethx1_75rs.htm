<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>CreateIndex Method Example (DAO/Microsoft Jet)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthcreateindexx"></a>CreateIndex Method Example</b></p>
<p>
This example uses the <b>CreateIndex</b> method to create two new <b>Index</b> objects and then appends them to the <b>Indexes</b> collection of the Employees <b>TableDef</b> object. It then enumerates the <b>Indexes</b> collection of the <b>TableDef</b> object, the <b>Fields</b> collection of the new <b>Index</b> objects, and the <b>Properties</b> collection of the new <b>Index</b> objects. The CreateIndexOutput function is required for this procedure to run.</p>
<pre><code>Sub CreateIndexX()

   Dim dbsNorthwind As Database
   Dim tdfEmployees As TableDef
   Dim idxCountry As Index
   Dim idxFirstName As Index
   Dim idxLoop As Index

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set tdfEmployees = dbsNorthwind!Employees

   With tdfEmployees
      ' Create first Index object, create and append Field
      ' objects to the Index object, and then append the
      ' Index object to the Indexes collection of the
      ' TableDef.
      Set idxCountry = .CreateIndex("CountryIndex")
      With idxCountry
         .Fields.Append .CreateField("Country")
         .Fields.Append .CreateField("LastName")
         .Fields.Append .CreateField("FirstName")
      End With
      .Indexes.Append idxCountry

      ' Create second Index object, create and append Field
      ' objects to the Index object, and then append the
      ' Index object to the Indexes collection of the
      ' TableDef.
      Set idxFirstName = .CreateIndex
      With idxFirstName
         .Name = "FirstNameIndex"
         .Fields.Append .CreateField("FirstName")
         .Fields.Append .CreateField("LastName")
      End With
      .Indexes.Append idxFirstName

      ' Refresh collection so that you can access new Index 
      ' objects.
      .Indexes.Refresh

      Debug.Print .Indexes.Count &amp; " Indexes in " &amp; _
         .Name &amp; " TableDef"

      ' Enumerate Indexes collection.
      For Each idxLoop In .Indexes
         Debug.Print "&nbsp; " &amp; idxLoop.Name
      Next idxLoop

      ' Print report.
      CreateIndexOutput idxCountry
      CreateIndexOutput idxFirstName

      ' Delete new Index objects because this is a
      ' demonstration.
      .Indexes.Delete idxCountry.Name
      .Indexes.Delete idxFirstName.Name
   End With

   dbsNorthwind.Close

End Sub

Function CreateIndexOutput(idxTemp As Index)

   Dim fldLoop As Field
   Dim prpLoop As Property

   With idxTemp
      ' Enumerate Fields collection of Index object.
      Debug.Print "Fields in " &amp; .Name
      For Each fldLoop In .Fields
         Debug.Print "&nbsp; " &amp; fldLoop.Name
      Next fldLoop

      ' Enumerate Properties collection of Index object.
      Debug.Print "Properties of " &amp; .Name
      For Each prpLoop In .Properties
         Debug.Print "&nbsp; " &amp; prpLoop.Name &amp; " - " &amp; _
            IIf(prpLoop = "", "[empty]", prpLoop)
      Next prpLoop
   End With

End Function</code></pre>
</font></BODY>
</HTML>
