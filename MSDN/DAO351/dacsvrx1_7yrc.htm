<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>CreateTableDef Method, FillCache Method, and CacheSize, CacheStart and SourceTableName Properties Example (Client/Server) [DAO/Microsoft Jet]</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthfillcachex"></a>CreateTableDef Method, FillCache Method, and CacheSize, CacheStart and SourceTableName Properties Example (Client/Server)</b></p>
<p>
This example uses the <b>CreateTableDef</b> and <b>FillCache</b> methods and the <b>CacheSize</b>, <b>CacheStart</b> and <b>SourceTableName</b> properties to enumerate the records in a <a href="dadefn01_0fad.htm">linked table</a> twice. Then it enumerates the records twice with a 50-record cache. The example then displays the performance statistics for the uncached and cached runs through the linked table.</p>
<pre><code>Sub ClientServerX3()

   Dim dbsCurrent As Database
   Dim tdfRoyalties As TableDef
   Dim rstRemote As Recordset
   Dim sngStart As Single
   Dim sngEnd As Single
   Dim sngNoCache As Single
   Dim sngCache As Single
   Dim intLoop As Integer
   Dim strTemp As String
   Dim intRecords As Integer

   ' Open a database to which a linked table can be 
   ' appended.
   Set dbsCurrent = OpenDatabase("DB1.mdb")

   ' Create a linked table that connects to a Microsoft SQL
   ' Server database.
   Set tdfRoyalties = _
      dbsCurrent.<span class=cfe><b>CreateTableDef</b></span>("Royalties")
   tdfRoyalties.Connect = _
      "ODBC;DATABASE=pubs;UID=sa;PWD=;DSN=Publishers"
   tdfRoyalties.<span class=cfe><b>SourceTableName</b></span> = "roysched"
   dbsCurrent.TableDefs.Append tdfRoyalties
   Set rstRemote = _
      dbsCurrent.OpenRecordset("Royalties")

   With rstRemote
      ' Enumerate the Recordset object twice and record
      ' the elapsed time.
      sngStart = Timer

      For intLoop = 1 To 2
         .MoveFirst
         Do While Not .EOF
            ' Execute a simple operation for the
            ' performance test.
            strTemp = !title_id
            .MoveNext
         Loop
      Next intLoop

      sngEnd = Timer
      sngNoCache = sngEnd - sngStart

      ' Cache the first 50 records.
      .MoveFirst
      .CacheSize = 50
      .<span class=cfe><b>FillCache</b></span>
      sngStart = Timer

      ' Enumerate the Recordset object twice and record
      ' the elapsed time.
      For intLoop = 1 To 2
         intRecords = 0
         .MoveFirst
         Do While Not .EOF
            ' Execute a simple operation for the
            ' performance test.
            strTemp = !title_id
            ' Count the records. If the end of the
            ' cache is reached, reset the cache to the
            ' next 50 records.
            intRecords = intRecords + 1
            .MoveNext
            If intRecords Mod 50 = 0 Then
               .CacheStart = .Bookmark
               .<span class=cfe><b>FillCache</b></span>
            End If
         Loop
      Next intLoop

      sngEnd = Timer
      sngCache = sngEnd - sngStart

      ' Display performance results.
      MsgBox "Caching Performance Results:" &amp; vbCr &amp; _
         "&nbsp; No cache: " &amp; Format(sngNoCache, _
         "##0.000") &amp; " seconds" &amp; vbCr &amp; _
         "&nbsp; 50-record cache: " &amp; Format(sngCache, _
         "##0.000") &amp; " seconds"
      .Close
   End With

   ' Delete linked table because this is a demonstration.
   dbsCurrent.TableDefs.Delete tdfRoyalties.Name
   dbsCurrent.Close

End Sub</code></pre>
</font></BODY>
</HTML>
