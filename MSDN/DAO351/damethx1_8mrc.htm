<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>GetRows Method Example (DAO)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthgetrowsx"></a>GetRows Method Example</b></p>
<p>
This example uses the <b>GetRows</b> method to retrieve a specified number of rows from a <b>Recordset</b> and to fill an array with the resulting data. The <b>GetRows</b> method will return fewer than the desired number of rows in two cases: either if <b>EOF</b> has been reached, or if <b>GetRows</b> tried to retrieve a record that was deleted by another user. The function returns <b>False</b> only if the second case occurs. The GetRowsOK function is required for this procedure to run.</p>
<pre><code>Sub GetRowsX()

   Dim dbsNorthwind As Database
   Dim rstEmployees As Recordset
   Dim strMessage As String
   Dim intRows As Integer
   Dim avarRecords As Variant
   Dim intRecord As Integer

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set rstEmployees = dbsNorthwind.OpenRecordset( _
      "SELECT FirstName, LastName, Title " &amp; _
      "FROM Employees ORDER BY LastName", dbOpenSnapshot)

   With rstEmployees
      Do While True
         ' Get user input for number of rows.
         strMessage = "Enter number of rows to retrieve."
         intRows = Val(InputBox(strMessage))

         If intRows &lt;= 0 Then Exit Do

         ' If GetRowsOK is successful, print the results,
         ' noting if the end of the file was reached.
         If GetRowsOK(rstEmployees, intRows, _
               avarRecords) Then
            If intRows &gt; UBound(avarRecords, 2) + 1 Then
               Debug.Print "(Not enough records in " &amp; _
                  "Recordset to retrieve " &amp; intRows &amp; _
                  " rows.)"
            End If
            Debug.Print UBound(avarRecords, 2) + 1 &amp; _
               " records found."

            ' Print the retrieved data.
            For intRecord = 0 To UBound(avarRecords, 2)
               Debug.Print "&nbsp; " &amp; _
                  avarRecords(0, intRecord) &amp; " " &amp; _
                  avarRecords(1, intRecord) &amp; ", " &amp; _
                  avarRecords(2, intRecord)
            Next intRecord
         Else
            ' Assuming the GetRows error was due to data 
            ' changes by another user, use Requery to
            ' refresh the Recordset and start over.
            If .Restartable Then
               If MsgBox("GetRows failed--retry?", _
                     vbYesNo) = vbYes Then
                  .Requery
               Else
                  Debug.Print "GetRows failed!"
                  Exit Do
               End If
            Else
               Debug.Print "GetRows failed! " &amp; _
                  "Recordset not Restartable!"
               Exit Do
            End If
         End If

         ' Because using GetRows leaves the current record 
         ' pointer at the last record accessed, move the
         ' pointer back to the beginning of the Recordset
         ' before looping back for another search.
         .MoveFirst
      Loop
   End With

   rstEmployees.Close
   dbsNorthwind.Close

End Sub

Function GetRowsOK(rstTemp As Recordset, _
   intNumber As Integer, avarData As Variant) As Boolean

   ' Store results of GetRows method in array.
   avarData = rstTemp.<i>GetRows</i>(intNumber)
   ' Return False only if fewer than the desired number of
   ' rows were returned, but not because the end of the 
   ' Recordset was reached.
   If intNumber &gt; UBound(avarData, 2) + 1 And _
         Not rstTemp.EOF Then
      GetRowsOK = False
   Else
      GetRowsOK = True
   End If

End Function</code></pre>
</font></BODY>
</HTML>
