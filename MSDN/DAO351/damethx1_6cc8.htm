<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>MoveFirst, MoveLast, MoveNext, MovePrevious Methods Example (DAO)</title>
<link disabled rel=stylesheet href=msdn_ie3.css>
<style type="text/css">
@import url(msdn_ie4.css);
</style>
</HEAD>
<BODY>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END-->


<p class=label>
<b><a name="damthmovefirstx"></a>MoveFirst, MoveLast, MoveNext, MovePrevious Methods Example</b></p>
<p>
This example uses the <b>MoveFirst</b>, <b>MoveLast</b>, <b>MoveNext</b>, and <b>MovePrevious </b>methods to move the record pointer of a <b>Recordset </b>based on the supplied command. The MoveAny procedure is required for this procedure to run.</p>
<pre><code>Sub MoveFirstX()

   Dim dbsNorthwind As Database
   Dim rstEmployees As Recordset
   Dim strMessage As String
   Dim intCommand As Integer

   Set dbsNorthwind = OpenDatabase("Northwind.mdb")
   Set rstEmployees = dbsNorthwind.OpenRecordset( _
      "SELECT FirstName, LastName FROM Employees " &amp; _
      "ORDER BY LastName", dbOpenSnapshot)

   With rstEmployees
      ' Populate Recordset.
      .<i>MoveLast</i>
      .<i>MoveFirst</i>
      Do While True
         ' Show current record information and get user's
         ' method choice.
         strMessage = "Name: " &amp; !FirstName &amp; " " &amp; _
            !LastName &amp; vbCr &amp; "Record " &amp; _
            (.AbsolutePosition + 1) &amp; " of " &amp; _
            .RecordCount &amp; vbCr &amp; vbCr &amp; _
            "[1 - MoveFirst, 2 - MoveLast, " &amp; vbCr &amp; _
            "3 - MoveNext, 4 - MovePrevious]"
         intCommand = Val(Left(InputBox(strMessage), 1))
         If intCommand &lt; 1 Or intCommand &gt; 4 Then Exit Do

         ' Call method based on user's input.
         MoveAny intCommand, rstEmployees
      Loop
      .Close
   End With

   dbsNorthwind.Close

End Sub

Sub MoveAny(intChoice As Integer, _
   rstTemp As Recordset)

   ' Use specified method, trapping for BOF and EOF.
   With rstTemp
      Select Case intChoice
         Case 1
            .<i>MoveFirst</i>
         Case 2
            .<i>MoveLast</i>
         Case 3
            .<i>MoveNext</i>
            If .EOF Then
               MsgBox "Already at end of recordset!"
               .<i>MoveLast</i>
            End If
         Case 4
            .<i>MovePrevious</i>
            If .BOF Then
               MsgBox "Already at beginning of recordset!"
               .<i>MoveFirst</i>
            End If
      End Select
   End With

End Sub</code></pre>
</font></BODY>
</HTML>
