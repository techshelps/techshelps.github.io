<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text-html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Determining Whether the Current Database Is Open Exclusively</title>
<style>@import url(msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="msdn_ie3.css"></HEAD>	<BODY bgcolor="#FFFFFF" link=#003399 vlink=#996699>

	<FONT FACE="Verdana, Arial, Helvetica" SIZE="2">

	<!--TOOLBAR_START-->
	<!--TOOLBAR_EXEMPT-->
	<!--TOOLBAR_END--><h4><a name="jb2determining_whether_the_current_database_is_open_exclusively"></a>Determining Whether the Current Database Is Open Exclusively</h4>
<p>
Determining whether the current database is open exclusively isn’t simply a matter of trying to open the database. If you have the database open exclusively and try to open it again, you won’t receive a run-time error as you may expect. Actually, you can open a database as many times as you want to, exclusively or not, with the same <b>DBEngine</b> object.</p>
<p>
To determine whether the current database is open exclusively, you can try to open the database by using the <b>PrivDBEngine</b> object, as shown in the following code:</p>
<pre><FONT FACE="Courier New" SIZE="3">Function IsDbOpenedExclusively(strDbPath As String) As Boolean
&#09;On Error Resume Next
&#09;Dim dbe As PrivDBEngine
&#09;Dim wrk As Workspace
&#09;Dim dbs As Database
&#09;Const conFileInUse = 3045
&#09;Const conDBOpenedExclusively = 3356

&#09;' Return reference to private DBEngine object.
&#09;Set dbe = New PrivDBEngine
&#09;' Return reference to default workspace.
&#09;Set wrk = dbe.Workspaces(0)
&#09;' Attempt to open database.
&#09;Set dbs = wrk.OpenDatabase(strDbPath)
&#09;' If reference to database isn't returned, check error.
&#09;If dbs Is Nothing Then
&#09;&#09;' If error indicates database is open exclusively, return True.
&#09;&#09;If (Err = conFileInUse Or Err = conDBOpenedExclusively) Then
&#09;&#09;&#09;IsDbOpenedExclusively = True
&#09;&#09;' If unanticipated error occurs, display message.
&#09;&#09;Else
&#09;&#09;&#09;MsgBox &quot;Error: &quot; &amp; Err &amp; &quot;: &quot; &amp; vbCrLf &amp; Err.Description
&#09;&#09;End If
&#09;' If reference to database is returned, it must not be opened
&#09;' exclusively by any other user.
&#09;Else
&#09;&#09;IsDbOpenedExclusively = False
&#09;&#09;dbs.Close
&#09;&#09;Set dbs = Nothing
&#09;End If
End Function</font></pre>
<p>
If this function generates error 3045 or error 3356, then it returns <b>True</b> because these errors indicate that the database is already opened exclusively.</p>
</BODY>
</HTML>
