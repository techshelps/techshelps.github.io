<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Sending a Data Recordset in a Message</title>
<link disabled rel=stylesheet href=../../backsdk3.css>
<style type="text/css">
@import url(../../backsdk4.css);
</style>
</HEAD>
<BODY BGCOLOR = #FFFFFF TEXT = #000000>

<h3><a name="_mq_sending_a_data_recordset_in_a_message_activex"></a>Sending a Data Recordset in a Message</h3>
<p>
Data recordsets can be sent and retrieved using a combination of MSMQ and Microsoft Remote Data Service (RDS). </p>
<p>
<b>Note</b>&nbsp;&nbsp;RDS is used to transport Active Data Object recordsets from a server to a client computer. The resulting recordset is cached on the client computer and disconnected from the server.</p>
<h5><img src="../../images/wedge.gif" border=0>&nbsp;&nbsp;&nbsp;&nbsp;To send a data recordset in a message</h5>
<ol>
<li>
Make sure RDS and Microsoft SQL Server are installed on your computer. (This example was tested using RDS 1.5 and SQL 6.5.)</li>
<li>
Make sure SQL Server services has been stated.</li>
<li>
Add references to the appropriate object libraries in your Microsoft Visual Basic project. This example was tested with the following libraries referenced.<ul>
<li>
Microsoft ActiveX Data Objects 1.5 Object Library</li>
<li>
Microsoft Remote Data Service Object Library</li>
</ul>
</li>
<li>
Create a destination queue and open it with send access.<pre><code>qinfo.PathName = ".\RecordsetQueue2"
qinfo.Label = "My Recordset Queue"
On Error Resume Next               'Ignore if queue already exists.
qinfo.Create
On Error Goto 0                    'Reset error handler.
Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
 </code></pre>
</li>
<li>
Create the recordset.<pre><code>sql = "select * from authors"
Set rsSend = dataFactory.Query("driver={SQL Server};server=.;database=pubs;uid=sa;pwd=", sql)
 </code></pre>
</li>
<li>
Create and send a message. Set the body of the message to the ADO recordset.<pre><code>mSend.Label = "Testing Recordset"
mSend.Body = rsSend
mSend.Send qSend
qSend.Close
 </code></pre>
</li>
<li>
Open the queue with receive access and retrieve the message.<pre><code>Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
Set mReceive = qReceive.Receive
 </code></pre>
</li>
<li>
Create a new recordset using the body of the retrieved message. The example below prints out the content of the recordset to confirm that the data was sent and retrieved.<pre><code>Set rsReceive = mReceive.Body
 </code></pre>
</li>
</ol>
<h4>Example Code</h4>
<p>
The following example creates an ADO recordset, sends the message to a destination queue, and retrieves the message and places it in a new recordset.</p>
<pre><code>Option Explicit

Dim rsSend As ADODB.Recordset
Dim rsReceive As ADODB.Recordset
Dim dataFactory As New RDSServer.DataFactory
Dim sql As String

Dim qinfo As New MSMQQueueInfo
Dim qSend As MSMQQueue
Dim qReceive As MSMQQueue
Dim mSend As New MSMQMessage
Dim mReceive As MSMQMessage


Private Sub Form_Click()
  
  '*******************************************************************
  ' Create a destination queue and open it with SEND access.
  '*******************************************************************
  qinfo.PathName = ".\RecordsetQueue2"
  qinfo.Label = "My Recordset Queue"
  On Error Resume Next                'Ignore if queue already exists.
  qinfo.Create
  On Error Goto 0                     'Reset error handler.
  Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
  
  '*******************************************************************
  ' Create an ADO recordset.
  '*******************************************************************
  sql = "select * from authors"
  Set rsSend = dataFactory.Query("driver={SQL Server};server=.;database=pubs;uid=sa;pwd=", sql)
  
  '*******************************************************************
  ' Create a message and send it to the destination queue. Set the 
  ' body of the message to the ADO recordset.
  '*******************************************************************
  mSend.Label = "Testing Recordset"
  mSend.Body = rsSend
  mSend.Send qSend
  qSend.Close
 
  '*******************************************************************
  ' Open the destination queue with RECEIVE access, and retrieve the
  ' message.
  '*******************************************************************
  Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
  Set mReceive = qReceive.Receive
 
  '*******************************************************************
  ' Create a new recordset using the body of the retrieved message.
  '*******************************************************************
  Set rsReceive = mReceive.Body
  Debug.Print rsReceive(0)                   ' Print column 1
  Debug.Print rsReceive(1)                   ' Print column 2
  Debug.Print rsReceive(2)                   ' Print column 3
 
  rsReceive.MoveNext
  Debug.Print rsReceive(0)                   ' Print column 1
  Debug.Print rsReceive(1)                   ' Print column 2
  Debug.Print rsReceive(2)                   ' Print column 3
 
End Sub
 </code></pre>
<p>&nbsp;</p></body>
</HTML>
