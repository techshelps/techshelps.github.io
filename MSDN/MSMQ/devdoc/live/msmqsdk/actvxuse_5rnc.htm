<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Sending a Persistent Object</title>
<link disabled rel=stylesheet href=../../backsdk3.css>
<style type="text/css">
@import url(../../backsdk4.css);
</style>
</HEAD>
<BODY BGCOLOR = #FFFFFF TEXT = #000000>

<h3><a name="_mq_sending_a_persistent_object_activex"></a>Sending a Persistent Object</h3>
<p>
MSMQ supports sending and receiving serialized objects that support <b>IPersistStream</b> and <b>IPersistStorage</b>. Many persistent objects, such as all Microsoft Office documents, can be sent as the body of an MSMQ message.</p>
<p>
When sending an object as the body of a message, MSMQ seamlessly invokes the object's IPersist interface. In this way, when the message is sent, the persistent state of the object is serialized into the message body using the object's supplied IPersist interface. At the receiving end, an implementation of the object's interface must be installed to use the object when it is removed from the queue.</p>
<p>
The example below sends a Microsoft Word 8.0 document to a destination queue. This code could be modified to send any persistent object.</p>
<h5><img src="../../images/wedge.gif" border=0>&nbsp;&nbsp;&nbsp;&nbsp;To send a Microsoft Word 8.0 document</h5>
<ol>
<li>
Make sure that Microsoft Word 8.0 is installed on your computer.</li>
<li>
Add a reference to Microsoft Word 8.0 in your Microsoft Visual Basic project. This example was tested with the following library referenced.<ul>
<li>
Microsoft Word (8.0) Object Library</li>
</ul>
</li>
<li>
Create a destination queue and open it with send access.<pre><code>qinfo.PathName = ".\Objecttest"
qinfo.Label = "My Object Test Queue"
On Error Resume Next           'Ignore if queue already exists.
qinfo.Create
On Error Goto 0                'Reset error handler.
Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
 </code></pre>
</li>
<li>
Create the Microsoft Word document object.<pre><code>Set msWordDoc = msWordApp.Documents.Open("FileName:=pathname\filename", ConfirmedConversions:=False, ReadOnly:=True)
 </code></pre>
</li>
<li>
Create and send a message. Set the body of the message to the document object.<pre><code>mSend.Label = "Testing Object Message"
mSend.Body = msWordDoc
mSend.Send qSend
qSend.Close
 </code></pre>
</li>
<li>
Open the queue with receive access and retrieve the message.<pre><code>Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
Set mReceive = qReceive.Receive
 </code></pre>
</li>
<li>
Create a new object using the body of the retrieved message. The example below then tests the new object to verify that it is a Microsoft Word document object.<pre><code>Set objReceive = mReceive.Body
 </code></pre>
</li>
</ol>
<h4>Example Code</h4>
<p>
The following example creates a Microsoft Word document object, sends the object as an MSMQ message to a destination queue, retrieves the message from the queue, and then creates a new object using the body of the retrieved message. The new object is testing to see if it is a Word document object.</p>
<pre><code>Option Explicit

Dim msWordApp As New Word.Application
Dim msWordDoc As Word.Document
Dim objReceive As Object

Dim qinfo As New MSMQQueueInfo
Dim qSend As MSMQQueue
Dim qReceive As MSMQQueue
Dim mSend As New MSMQMessage
Dim mReceive As MSMQMessage


Private Sub Form_Click()
  
  '*******************************************************************
  ' Create a destination queue and open it with SEND access.
  '*******************************************************************
  qinfo.PathName = ".\ObjectTest"
  qinfo.Label = "My Object Test Queue"
  On Error Resume Next           'Ignore if queue already exists.
  qinfo.Create
  On Error GoTo 0
  Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
  
  '*******************************************************************
  ' Create document object.
  '*******************************************************************
    Set msWordDoc = msWordApp.Documents.Open("FileName:=pathname\filename", ConfirmedConversions:=False, ReadOnly:=True)
  
  '*******************************************************************
  ' Create a message and send it to the destination queue. Set the 
  ' body of the message to the document object.
  '*******************************************************************
  mSend.Label = "Testing Object Message"
  mSend.Body = msWordDoc
  mSend.Send qSend
  qSend.Close
 
  MsgBox "Document object sent as body of message."
 
  '*******************************************************************
  ' Open the destination queue with Receive access, and retrieve the
  ' message.
  '*******************************************************************
  Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
  Set mReceive = qReceive.Receive
  qReceive.Close
   
  '*******************************************************************
  ' Retrieve the message from the queue and create new document
  ' object. Test the object to verify it is a Microsoft Word document.
  '*******************************************************************
  Set objReceive = mReceive.Body
  If TypeOf objReceive Is Word.Document Then
     MsgBox "Received message body is a Microsoft Word document."
  Else
     MsgBox "Received message body is unknown object."
  End If
  
End Sub
 </code></pre>
<p>&nbsp;</p></body>
</HTML>
