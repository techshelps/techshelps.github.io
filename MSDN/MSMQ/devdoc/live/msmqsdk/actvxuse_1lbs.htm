<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><title>Sending an Array of Bytes in a Message</title>
<link disabled rel=stylesheet href=../../backsdk3.css>
<style type="text/css">
@import url(../../backsdk4.css);
</style>
</HEAD>
<BODY BGCOLOR = #FFFFFF TEXT = #000000>

<h3><a name="_mq_sending_an_array_of_bytes_in_a_message_activex"></a>Sending an Array of Bytes in a Message</h3>
<p>
Sending messages consists of opening a queue with send access, creating the message, calling the <a href="mqactx_7k2s.htm"><b>Send</b></a> method, and then closing the queue. The following procedure and example code show how this is done when sending a message whose body contains an array of bytes.</p>
<h5><img src="../../images/wedge.gif" border=0>&nbsp;&nbsp;&nbsp;&nbsp;To send an array of bytes in a message body</h5>
<ol>
<li>
Declare QueueInfo, Queue, and Message objects. This example uses one set of Queue and Message objects for sending the message and another set of Queue and Message objects for receiving the message.<pre><code>Dim qinfo As New MSMQQueueInfo
Dim qSend As MSMQQueue
Dim qReceive As MSMQQueue
Dim mSend As New MSMQMessage
Dim mReceive As MSMQMessage
 </code></pre>
</li>
<li>
Create a destination queue and open it with send access. This example creates a public queue on the local computer.<pre><code>qinfo.PathName = ".\SendTest"
qinfo.Label = "Send Message Test"
On Error Resume Next          'Ignore if queue already exists.
qinfo.Create
On Error GoTo 0
Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
 </code></pre>
</li>
<li>
Send the message with an array of bytes in the message body.<pre><code>Dim aByte(5) As Byte
Dim Counter As Integer

'Populate the array
For Counter = LBound(aByte) To UBound(aByte)
    aByte(Counter) = CByte(Counter)
Next

mSend.Label = "Byte Array Message"
mSend.Body = aByte
mSend.Send qSend
qSend.Close
 </code></pre>
</li>
<li>
Open the destination queue with receive access and retrieve the message. This example removes the message from the queue.<pre><code>Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
Set mReceive = qReceive.Receive
qReceive.Close
 </code></pre>
</li>
<li>
Verify that the body of the retrieved message body is an array of bytes.<pre><code>If TypeName(mReceive.Body) = "Byte()" Then
   MsgBox "The retrieved message body is an array of bytes."
   Else
   MsgBox "The retrieved message body is not an array of bytes."
End If
 </code></pre>
</li>
</ol>
<h4>Example Code</h4>
<p>
The following example creates a destination queue, sends a message whose body contains a byte array, retrieves the message, and tests the retrieved message body to see if it is an array of bytes.</p>
<pre><code>Option Explicit

Dim qinfo As New MSMQQueueInfo
Dim qSend As MSMQQueue
Dim qReceive As MSMQQueue
Dim mSend As New MSMQMessage
Dim mReceive As MSMQMessage

Private Sub Form_Click()
  
  '*************************************************************
  ' Create a destination queue and open it with SEND access.
  '*************************************************************
  qinfo.PathName = ".\SendTest"
  qinfo.Label = "Send Message Test"
  On Error Resume Next          'Ignore if queue already exists.
  qinfo.Create
  On Error GoTo 0
  Set qSend = qinfo.Open(MQ_SEND_ACCESS, MQ_DENY_NONE)
  
  '*************************************************************
  ' Send message with an array of bytes.
  '*************************************************************
  Dim aByte(5) As Byte
  Dim Counter As Integer
  
  'Populate the array
  For Counter = LBound(aByte) To UBound(aByte)
      aByte(Counter) = CByte(Counter)
  Next

  mSend.Label = "Array of Bytes Message"
  mSend.Body = aByte
  mSend.Send qSend
  qSend.Close
    
  '*************************************************************
  ' Open the destination queue with RECEIVE access, and
  ' retrieve the message. The retrieved message body is tested
  ' to verify it is an array of bytes.
  '*************************************************************
  Set qReceive = qinfo.Open(MQ_RECEIVE_ACCESS, MQ_DENY_NONE)
  Set mReceive = qReceive.Receive
  qReceive.Close
  
  If TypeName(mReceive.Body) = "Byte()" Then
     MsgBox "The retrieved message body is an array of bytes."
     Else
     MsgBox "The retrieved message body is not an array of bytes."
  End If
  
End Sub
 </code></pre>
<p>&nbsp;</p></body>
</HTML>
