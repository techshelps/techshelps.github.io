<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>AddressOf Operator Example</TITLE>
<style>@import url(office.css);</style>
	<link disabled rel="stylesheet" href="msoffice.css"></HEAD>

<BODY BGCOLOR="#FFFFFF" TEXT="#000000">



<H2><A NAME="vaopraddressofx"></A>AddressOf Operator Example</H2>

<P class=T>The following example creates a form with a list box containing an alphabetically sorted list of the fonts in your system.</P>

<P class=T>To run this example, create a form with a list box on it. The code for the form is as follows:</P>

<PRE class=CT><CODE>Option Explicit

Private Sub Form_Load()
    Module1.FillListWithFonts List1
End Sub
</CODE></PRE>

<P class=T>Place the following code in a module. The third argument in the definition of the EnumFontFamilies function is a <B>Long</B> that represents a procedure. The argument must contain the address of the procedure, rather than the value that the procedure returns. In the call to EnumFontFamilies, the third argument requires the <B>AddressOf</B> operator to return the address of the EnumFontFamProc procedure, which is the name of the callback procedure you supply when calling the Windows API function, <B>EnumFontFamilies</B>. Windows calls EnumFontFamProc once for each of the font families on the system when you pass <B>AddressOf</B> EnumFontFamProc to <B>EnumFontFamilies</B>. The last argument passed to <B>EnumFontFamilies</B> specifies the list box in which the information is displayed.</P>

<PRE class=CT><CODE>'Font enumeration types
Public Const LF_FACESIZE = 32
Public Const LF_FULLFACESIZE = 64

Type LOGFONT
        lfHeight As Long
        lfWidth As Long
        lfEscapement As Long
        lfOrientation As Long
        lfWeight As Long
        lfItalic As Byte
        lfUnderline As Byte
        lfStrikeOut As Byte
        lfCharSet As Byte
        lfOutPrecision As Byte
        lfClipPrecision As Byte
        lfQuality As Byte
        lfPitchAndFamily As Byte
        lfFaceName(LF_FACESIZE) As Byte
End Type

Type NEWTEXTMETRIC
        tmHeight As Long
        tmAscent As Long
        tmDescent As Long
        tmInternalLeading As Long
        tmExternalLeading As Long
        tmAveCharWidth As Long
        tmMaxCharWidth As Long
        tmWeight As Long
        tmOverhang As Long
        tmDigitizedAspectX As Long
        tmDigitizedAspectY As Long
        tmFirstChar As Byte
        tmLastChar As Byte
        tmDefaultChar As Byte
        tmBreakChar As Byte
        tmItalic As Byte
        tmUnderlined As Byte
        tmStruckOut As Byte
        tmPitchAndFamily As Byte
        tmCharSet As Byte
        ntmFlags As Long
        ntmSizeEM As Long
        ntmCellHeight As Long
        ntmAveWidth As Long
End Type

' ntmFlags field flags
Public Const NTM_REGULAR = &amp;H40&amp;
Public Const NTM_BOLD = &amp;H20&amp;
Public Const NTM_ITALIC = &amp;H1&amp;

'  tmPitchAndFamily flags
Public Const TMPF_FIXED_PITCH = &amp;H1
Public Const TMPF_VECTOR = &amp;H2
Public Const TMPF_DEVICE = &amp;H8
Public Const TMPF_TRUETYPE = &amp;H4

Public Const ELF_VERSION = 0
Public Const ELF_CULTURE_LATIN = 0

'  EnumFonts Masks
Public Const RASTER_FONTTYPE = &amp;H1
Public Const DEVICE_FONTTYPE = &amp;H2
Public Const TRUETYPE_FONTTYPE = &amp;H4

Declare Function EnumFontFamilies Lib "gdi32" Alias _
     "EnumFontFamiliesA" _
     (ByVal hDC As Long, ByVal lpszFamily As String, _ 
     ByVal lpEnumFontFamProc As Long, LParam As Any) As Long
Declare Function GetDC Lib "user32" (ByVal hWnd As Long) As Long
Declare Function ReleaseDC Lib "user32" (ByVal hWnd As Long, _
     ByVal hDC As Long) As Long

Function EnumFontFamProc(lpNLF As LOGFONT, lpNTM As NEWTEXTMETRIC, _ 
     ByVal FontType As Long, LParam As ListBox) As Long
Dim FaceName As String
Dim FullName As String
    FaceName = StrConv(lpNLF.lfFaceName, vbUnicode)
    LParam.AddItem Left$(FaceName, InStr(FaceName, vbNullChar) - 1)
    EnumFontFamProc = 1
End Function

Sub FillListWithFonts(LB As ListBox)
Dim hDC As Long
    LB.Clear
    hDC = GetDC(LB.hWnd)
    EnumFontFamilies hDC, vbNullString, <B>AddressOf</B> EnumFontFamProc, LB
    ReleaseDC LB.hWnd, hDC
End Sub
</CODE></PRE>

</BODY>
</HTML>
