<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>Sample XSLT File for Sorting</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/xmlsdk.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannertitle">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannertitle" nowrap>Microsoft XML Core Services (MSXML) 5.0 for Microsoft Office - XSLT Developer's Guide</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="xmconsamplexsltfileforsorting"></A>Sample XSLT File for Sorting</H1>

<P>This is the complete final file for the topics in the <A HREF="xsl_advanced1_7i2b.htm">Sorting on Calculated Variables</A> section. Other topics in the <A HREF="xsl_advanced1_2juc.htm">Sorting with XSLT</A> section use different versions of the <B>prodsort<I>x</I>.xsl</B> file.</P>

<P class="label"><B>XSLT File (prodsortcvar.xsl)</B></P>

<PRE class="code">&lt;?xml version="1.0"?&gt;
&lt;xsl:stylesheet version="1.0"
   xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
   xmlns:msxsl="urn:schemas-microsoft-com:xslt"&gt;

&lt;xsl:template match="/"&gt;
   &lt;HTML&gt;
      &lt;HEAD&gt;
         &lt;TITLE&gt;Wooden Rings and More!&lt;/TITLE&gt;
         &lt;STYLE type="text/css"&gt;
            th {background-color: silver;
               font-family: Tahoma,Verdana,Arial,sans-serif}
            td {background-color: white;
               font-family: Tahoma,Verdana,Arial,sans-serif}
         &lt;/STYLE&gt;
      &lt;/HEAD&gt;
      &lt;BODY&gt;
         &lt;xsl:apply-templates/&gt;
      &lt;/BODY&gt;
   &lt;/HTML&gt;
&lt;/xsl:template&gt;

&lt;xsl:template match="products"&gt;
   &lt;!-- Create a variable to hold the generated result tree fragment. --&gt;
   &lt;xsl:variable name="prods_with_usd"&gt;
      &lt;xsl:apply-templates select="product" mode="calc_usd" /&gt;
   &lt;/xsl:variable&gt;
   &lt;TABLE width="75%"&gt;
      &lt;tr&gt;
         &lt;th&gt;Category&lt;/th&gt;
         &lt;th&gt;Prod ID&lt;/th&gt;
         &lt;th&gt;Name/Version&lt;/th&gt;
         &lt;th&gt;Description&lt;/th&gt;
         &lt;th&gt;Price (Currency)&lt;/th&gt;
         &lt;th&gt;Price (USD)&lt;/th&gt;
      &lt;/tr&gt;
      &lt;!-- Fill remainder of table with source tree "clone". --&gt;      
      &lt;xsl:apply-templates select="msxsl:node-set($prods_with_usd)/product"&gt;
         &lt;xsl:sort select="usd_equiv" data-type="number" /&gt;
      &lt;/xsl:apply-templates&gt;
   &lt;/TABLE&gt;
&lt;/xsl:template&gt;

&lt;xsl:template match="product" mode="calc_usd"&gt;
   &lt;xsl:copy&gt;
      &lt;xsl:copy-of select="@*" /&gt;
      &lt;xsl:copy-of select="*" /&gt;
      &lt;!-- Add a calculated &lt;usd_equiv&gt; child to each &lt;product&gt;. --&gt;      
      &lt;xsl:element name="usd_equiv"&gt;
         &lt;xsl:choose&gt;
            &lt;!-- Note: Exchange rates current as of Oct. 2000. --&gt;
            &lt;xsl:when test="price/@curr='USD'"&gt;
               &lt;xsl:value-of select="format-number(price, '#,##0.00')"/&gt;
            &lt;/xsl:when&gt;
            &lt;xsl:when test="price/@curr='GBP'"&gt;
               &lt;xsl:value-of select="format-number(price * 1.47275, '#,##0.00')"/&gt;
            &lt;/xsl:when&gt;
            &lt;xsl:when test="price/@curr='EU'"&gt;
               &lt;xsl:value-of select="format-number(price * 0.864379, '#,##0.00')"/&gt;
            &lt;/xsl:when&gt;
            &lt;xsl:otherwise&gt;Unknown Currency&lt;/xsl:otherwise&gt;
         &lt;/xsl:choose&gt;
      &lt;/xsl:element&gt;
   &lt;/xsl:copy&gt;
&lt;/xsl:template&gt;

&lt;xsl:template match="product"&gt;
   &lt;tr&gt;
      &lt;td valign="top"&gt;
         &lt;xsl:value-of select="categ"/&gt;
      &lt;/td&gt;
      &lt;td valign="top"&gt;
         &lt;xsl:value-of select="@prodID"/&gt;
      &lt;/td&gt;
      &lt;td valign="top"&gt;
         &lt;xsl:value-of select="concat(name, '/', version)"/&gt;
      &lt;/td&gt;
      &lt;td valign="top"&gt;
         &lt;xsl:value-of select="descr"/&gt;
      &lt;/td&gt;
      &lt;td valign="top" align="center"&gt;
         &lt;xsl:value-of select="concat(price, ' (', price/@curr, ')')"/&gt;
      &lt;/td&gt;
      &lt;td valign="top" align="right"&gt;
         &lt;xsl:value-of select="usd_equiv"/&gt;
      &lt;/td&gt;
   &lt;/tr&gt;
&lt;/xsl:template&gt;

&lt;/xsl:stylesheet&gt;</PRE>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
</BODY>
</HTML>
