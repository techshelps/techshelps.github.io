<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><TITLE>Visual Basic Code (importNode.frm)</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/xmlsdk.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannertitle">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannertitle" nowrap>Microsoft XML Core Services (MSXML) 5.0 for Microsoft Office - DOM Reference</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="domexamples_importnodefrm"></A>Visual Basic Code (importNode.frm)</H1>

<PRE class="code">Private Sub Form_Load()

Dim domFree As FreeThreadedDOMDocument50
Dim domApt As DOMDocument50
Dim node As IXMLDOMNode
Dim clone As IXMLDOMNode
Dim msg As String

msg = ""
Set domFree = New FreeThreadedDOMDocument50
Set domApt = New DOMDocument50

domApt.async = False
If False = domApt.Load(App.Path + "\doc1.xml") Then
    MsgBox "can't load doc1.xml"
    Exit Sub
End If

domFree.async = False
If False = domFree.Load(App.Path + "\doc2.xml") Then
    MsgBox "can't load doc2.xml"
    Exit Sub
End If

' Copy a node from domFree to domApt:
'   Fetch the "/doc" (node) from domFree (doc2.xml).
'   Clone node for import to domApt.
'   Append clone to domApt (doc1.xml).
'
Set node = domFree.selectSingleNode("/doc")
Set clone = domApt.importNode(node, True)
domApt.documentElement.appendChild clone
domApt.documentElement.appendChild domApt.createTextNode(vbNewLine)
msg = msg + "doc1.xml after importing /doc from doc2.xml:"
msg = msg + vbNewLine + domApt.xml + vbNewLine
Set node = Nothing
Set clone = Nothing

' Clone a node using importNode() and append it to self:
'   Fetch the "doc/b" (node) from domApt (doc1.xml).
'   Clone node using importNode() on domApt.
'   Append clone to domApt (doc1.xml).
'
Set node = domApt.selectSingleNode("/doc/b")
Set clone = domApt.importNode(node, True)
domApt.documentElement.appendChild domApt.createTextNode(vbTab)
domApt.documentElement.appendChild clone
msg = msg + "doc1.xml after import /doc/b from self:"
msg = msg + vbNewLine + domApt.xml + vbNewLine
Set node = Nothing
Set clone = Nothing

' Clone a node and append it to the dom using cloneNode():
'   Fetch "doc/a" (node) from domApt (doc1.xml).
'   Clone node using cloneNode on domApt.
'   Append clone to domApt (doc1.xml).
'
Set node = domApt.selectSingleNode("/doc/a")
Set clone = node.cloneNode(True)
domApt.documentElement.appendChild clone
msg = msg + "doc1.xml after cloning /doc/a from self:"
msg = msg + vbNewLine + domApt.xml + vbNewLine
Set node = Nothing
Set clone = Nothing

domApt.save App.Path + "\out.xml"
msg = msg + "a new document was saved to out.xml in the current working directory."

MsgBox msg
End Sub</PRE>

<P class="labelproc"><B>Try It!</B>

<OL type="1">
	<LI>Copy the first XML data (<A HREF="xml_mth_hn_1jqd.htm">doc1.xml</A>), and paste it into a text file. Save the file as <B>doc1.xml</B>.</li>

	<LI>Copy the second XML listing (<A HREF="xml_mth_hn_1jqd.htm">doc2.xml</A>), and paste it into a text file. Save the file as <B>doc2.xml</B>, in the same directory where you saved <B>doc1.xml</B>.</li>

	<LI>Create a Standard EXE project in Visual Basic. Save the empty project as <B>importNode.vbp</B> to the same directory where you saved <B>doc1.xml</B> and <B>doc2.xml</B>. Name the form file <B>importNode.frm</B>.</li>

	<LI>Create a reference to MSXML 5.0. To do this, select <B>References...</B> from the <B>Project</B> menu, then check the box for <B>Microsoft XML, v50</B>.</li>

	<LI>Copy the Visual Basic code listing above, and paste it into the <B>form_load</B> subroutine. </li>

	<LI>Execute the code by selecting <B>Start</B> from the <B>Run</B> menu.</li>

	<LI>Verify that the output is the same as that listed in <A HREF="xml_mth_hn_1lis.htm">Output for the importNode Example</A>.</li>
</OL>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
</BODY>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script></html>
