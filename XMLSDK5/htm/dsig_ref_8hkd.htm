<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><TITLE>Visual Basic Source: createSAXProxy.frm</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/xmlsdk.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannertitle">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannertitle" nowrap>Microsoft XML Core Services (MSXML) 5.0 for Microsoft Office - Digital Signatures</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="dsigexamples_createsaxproxyfrm"></A>Visual Basic Source: createSAXProxy.frm</H1>

<PRE class="code">Dim xmldoc As New DOMDocument50
Dim xmldsig As New MXDigitalSignature50
Dim dsigKey As IXMLDSigKey
Dim saxReader As New SAXXMLReader50
Dim saxProxy, outfile, provType, keyContainer

Const DSIGNS = "xmlns:ds='http://www.w3.org/2000/09/xmldsig#'"
Const PROV_RSA_FULL = 1
' Change this key container name to your own if necessary.
Const RSA_KEY = "MyRSAFullKeys"
Const INFILE = "signature_template.rsa.xml"
Const dataID = "#objData"
    
Private Function LoadXML(ByVal file As String)
    ' Read the input XML file.
    Path = App.Path + "\" + file
    xmldoc.async = False
    xmldoc.preserveWhiteSpace = True
    xmldoc.validateOnParse = False
    xmldoc.resolveExternals = False
    If xmldoc.Load(Path) = False Then
        WriteLine "Can't load " + Path
        WriteLine "Reason: " + xmldoc.parseError.reason
        LoadXML = False
        Exit Function
    End If
    
    ' Set the signature property on xmldsig.
    xmldoc.setProperty "SelectionNamespaces", DSIGNS
    Dim sig
    Set sig = xmldoc.selectSingleNode(".//ds:Signature")
    If sig Is Nothing Then
        WriteLine "failed select &lt;Signature&gt;"
        LoadXML = False
        Exit Function
    End If
    Set xmldsig.signature = sig
    
    LoadXML = True
End Function

Private Function SignXML(ByVal dataID As String, ByVal srcUrl As String)
    If xmldsig.signature Is Nothing Then
        WriteLine "Invalid signature template."
        SignXML = False
        Exit Function
    End If
    
    If keyContainer = "" Then
        WriteLine "Invalid key container."
        SignXML = False
        Exit Function
    End If
    
    ' Get the Key from the default csp provder.
    Set oKey = xmldsig.createKeyFromCSP(provType, "", keyContainer, 0)
    If oKey Is Nothing Then
        WriteLine "Invalid key"
        SignXML = False
        Exit Function
    End If
    
    If srcUrl &lt;&gt; "" Then
       ' Reassign the data source.
       Set saxProxy = xmldsig.createSAXProxy
       If saxProxy Is Nothing Then
          SignXML = False
          Exit Function
       End If
       Set saxReader.contentHandler = saxProxy
       xmldsig.setReferenceData dataID, saxProxy
       saxReader.parseURL srcUrl
    End If

CONTINUE:
    Set oSignedKey = xmldsig.Sign(oKey, PURGE)
    If oSignedKey Is Nothing Then
        WriteLine "sign failed."
        SignXML = False
        Exit Function
    End If
         
    WriteLine "Signing was succesfful."
    WriteLine "Resultant signature: " + vbNewLine
    WriteLine xmldoc.xml
    
    SignXML = True

End Function

Private Sub Form_Load()
    
    'Resize the text box control to the size of the form
    Text1.Top = 100
    Text1.Left = 100
    Text1.Width = Form1.Width - 350
    Text1.Height = Form1.Height - 750
    Dim newSrc As String
    provType = PROV_RSA_FULL
    keyContainer = RSA_KEY
    writeClear
    refID = "#objData"
    
    If LoadXML(INFILE) = True Then
       WriteLine "Signing data referenced in the signature..."
       SignXML refID, ""
       
       newSrc = App.Path + "\test.xml"
       WriteLine vbNewLine
       WriteLine "Signing " + newSrc + " fed through SAX Proxy..."
       LoadXML INFILE
       SignXML refID, newSrc
    End If
        
End Sub

Private Sub Form_Resize()
    'Resize the text box control to the size of the form
    Text1.Width = Form1.Width - 350
    Text1.Height = Form1.Height - 750
End Sub

' Helper function. Make sure that a TextBox control named Text1
' is present in the project.
Private Function WriteLine(ByVal str As String)
    Text1.Text = Text1.Text + str + vbNewLine
End Function

' Helper function:
Private Function writeClear()
    Text1.Text = ""
End Function</PRE>

<P class="labelproc"><B>Try It!</B>

<OL type="1">
	<LI>Ensure that you have completed all the procedures in <A HREF="dsig_guide_4mjo.htm">Getting Started with XML Digital Signatures</A>.</li>

	<LI>Copy the XML signature file <A HREF="dsig_ref_3t7p.htm">resource file</A>, and paste it into a text file. Save the file as <B>signature_template.rsa.xml</B>.</li>

	<LI>Copy the XML data file <A HREF="dsig_ref_3t7p.htm">resource file</A>, and paste it into a text file. Save the file as <B>test.xml</B>, in the same directory where you saved <B>signature_template.rsa.xml</B>.</li>

	<LI>Create a Standard EXE project in Visual Basic. Save the empty project as <B>createSAXProxy.vbp</B> to the same directory where you saved <B>signature_template.rsa.xml</B> and <B>test.xml</B>. Name the form file <B>createSAXProxy.frm</B>.</li>

	<LI>Create a reference to MSXML 5.0. To do this, select <B>References...</B> from the <B>Project</B> menu, then check the box for <B>Microsoft XML, v5.0</B>.</li>

	<LI>Copy the Visual Basic code listing above, and paste it into the code editor to replace whatever content is already there. </li>

	<LI>Place a <B>TextBox</B> control onto the form. It should be named as <B>Text1 </B>(the default name)<B>.</B> Click it to select it in the Properties window, and then modify the following properties for Text1: set the <B>ScrollBars</B> property to "2 &#0150; Vertical" and the <B>MultiLine</B> Property to True.</li>

	<LI>Execute the code by selecting <B>Start</B> from the <B>Run</B> menu.</li>

	<LI>Verify that your output is similar to that listed in the <A HREF="dsig_ref_2x84.htm">Output</A> topic. You can resize the form as needed to more easily see the output.</li>
</OL>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
</BODY>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script></html>
