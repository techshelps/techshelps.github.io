<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><TITLE>Sample XSLT File for Navigating XPath Axes</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/xmlsdk.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannertitle">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannertitle" nowrap>Microsoft XML Core Services (MSXML) 5.0 for Microsoft Office - XPath Developer's Guide</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="xmconsamplexsltfilefornavigatingxpathaxes"></A>Sample XSLT File for Navigating XPath Axes</H1>

<P>A subset of this file is used in each topic for XPath navigation axes.</P>

<P class="label"><B>XSLT File (orgchart.xsl)</B></P>

<PRE class="code">&lt;?xml version="1.0"?&gt;
&lt;xsl:stylesheet 
   xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
   version="1.0"&gt;

&lt;!-- Common template rule (suppresses all text nodes not 
   explicitly covered in succeeding template rules) --&gt;
&lt;xsl:template match="text()"/&gt;

&lt;!-- Parent axis --&gt;
&lt;xsl:template match="director[name='Michelle Votava']"&gt;
   &lt;table border="1"&gt;
      &lt;tr&gt;
         &lt;th&gt;Attribute name&lt;/th&gt;
         &lt;th&gt;Attribute value&lt;/th&gt;
         &lt;th&gt;Parent name&lt;/th&gt;
         &lt;th&gt;Parent value&lt;/th&gt;
      &lt;/tr&gt;
          &lt;xsl:for-each select="attribute::*"&gt;
             &lt;tr&gt;
               &lt;td&gt;&lt;xsl:value-of select="name()"/&gt;&lt;/td&gt;
               &lt;td&gt;&lt;xsl:value-of select="."/&gt;&lt;/td&gt;
               &lt;td&gt;&lt;xsl:value-of select="name(parent::*)"/&gt;&lt;/td&gt;
               &lt;td&gt;&lt;xsl:value-of select="parent::*"/&gt;&lt;/td&gt;
             &lt;/tr&gt;
          &lt;/xsl:for-each&gt;
   &lt;/table&gt;
&lt;/xsl:template&gt;

&lt;!-- Ancestor axis --&gt;
&lt;xsl:template match="*[name='Peter Porzuczek']"&gt;
   &lt;h2&gt;Peter Porzuczek's superiors:&lt;/h2&gt;
       &lt;xsl:for-each select="ancestor::*"&gt;
          &lt;h4&gt;&lt;xsl:value-of select="name"/&gt;&lt;/h4&gt;
       &lt;/xsl:for-each&gt;
&lt;/xsl:template&gt;

&lt;!-- Following axis --&gt;
&lt;xsl:template match="name[.='Katie McAskill-White']"&gt;
   &lt;h2&gt;Katie McAskill-White's followers:&lt;/h2&gt;
       &lt;xsl:for-each select="following::*/name"&gt;
          &lt;h4&gt;&lt;xsl:value-of select="."/&gt;&lt;/h4&gt;
       &lt;/xsl:for-each&gt;
&lt;/xsl:template&gt;

&lt;!-- Preceding axis --&gt;
&lt;xsl:template match="name[.='John Tippett']"&gt;
   &lt;table border="1"&gt;
      &lt;tr&gt;
         &lt;th&gt;Node name&lt;/th&gt;
         &lt;th&gt;Node value&lt;/th&gt;
      &lt;/tr&gt;
          &lt;xsl:for-each select="preceding::*"&gt;
         &lt;xsl:variable name="bgcolor"&gt;
                &lt;xsl:choose&gt;
                   &lt;xsl:when test="name()='name'"&gt;yellow&lt;/xsl:when&gt;
                   &lt;xsl:otherwise&gt;white&lt;/xsl:otherwise&gt;
                &lt;/xsl:choose&gt;
         &lt;/xsl:variable&gt;
             &lt;tr&gt;
                &lt;xsl:attribute name="style"&gt;background-color: &lt;xsl:value-of select="$bgcolor"/&gt;
                &lt;/xsl:attribute&gt;
                &lt;td&gt;
                   &lt;xsl:value-of select="name()"/&gt;
                &lt;/td&gt;
                &lt;td&gt;
                   &lt;xsl:value-of select="."/&gt;
                &lt;/td&gt;
             &lt;/tr&gt;
          &lt;/xsl:for-each&gt;
       &lt;/table&gt;
&lt;/xsl:template&gt;

&lt;!-- Following-sibling axis --&gt;
&lt;xsl:template match="name[.='Shelly Szymanski']"&gt;
   &lt;xsl:if test="following-sibling::*"&gt;
      &lt;h2&gt;following-siblings of &amp;lt;name&amp;gt; 
         '&lt;xsl:value-of select="."/&gt;'&lt;/h2&gt;
      &lt;table border="1"&gt;
         &lt;tr&gt;
            &lt;th&gt;Node name&lt;/th&gt;
            &lt;th&gt;Node value&lt;/th&gt;
         &lt;/tr&gt;
             &lt;xsl:for-each select="following-sibling::*"&gt;
                &lt;tr&gt;
                   &lt;td&gt;&lt;xsl:value-of select="name()"/&gt;&lt;/td&gt;
                   &lt;td&gt;&lt;xsl:value-of select="."/&gt;&lt;/td&gt;
                &lt;/tr&gt;
             &lt;/xsl:for-each&gt;
          &lt;/table&gt;
       &lt;/xsl:if&gt;
&lt;/xsl:template&gt;

&lt;!-- Preceding-sibling axis --&gt;
&lt;xsl:template match="chairman"&gt;
   &lt;xsl:if test="preceding-sibling::node()"&gt;
      &lt;h2&gt;preceding-siblings of '&lt;xsl:value-of select="name"/&gt;'&lt;/h2&gt;
   &lt;table border="1"&gt;
         &lt;tr&gt;
            &lt;th&gt;Node name&lt;/th&gt;
            &lt;th&gt;Node value&lt;/th&gt;
      &lt;/tr&gt;
      &lt;xsl:for-each select="preceding-sibling::node()"&gt;
          &lt;tr&gt;
            &lt;td&gt;&lt;xsl:value-of select="name()"/&gt;&lt;/td&gt;
             &lt;td&gt;&lt;xsl:value-of select="."/&gt;&lt;/td&gt;
          &lt;/tr&gt;
       &lt;/xsl:for-each&gt;
       &lt;/table&gt;
   &lt;/xsl:if&gt;
   &lt;xsl:apply-templates/&gt;
&lt;/xsl:template&gt;

&lt;!-- Attribute axis (note: two template rules) --&gt;
&lt;xsl:template match="/"&gt;
   &lt;html&gt;
      &lt;head&gt;&lt;title&gt;Employee Info&lt;/title&gt;&lt;/head&gt;
           &lt;body&gt;
            &lt;table border="1"&gt;
            &lt;tr&gt;
               &lt;th&gt;Name&lt;/th&gt;
               &lt;th&gt;Emp ID&lt;/th&gt;
               &lt;th&gt;Empl Date&lt;/th&gt;
            &lt;/tr&gt;
            &lt;xsl:apply-templates /&gt;
         &lt;/table&gt;
           &lt;/body&gt;
   &lt;/html&gt;
&lt;/xsl:template&gt;

&lt;xsl:template match="name"&gt;
   &lt;tr&gt;
      &lt;td&gt;&lt;xsl:value-of select="."/&gt;&lt;/td&gt;
      &lt;td&gt;&lt;xsl:value-of select="parent::*/attribute::empID"/&gt;&lt;/td&gt;
      &lt;td&gt;&lt;xsl:value-of select="parent::*/attribute::empdate"/&gt;&lt;/td&gt;
   &lt;/tr&gt;
&lt;/xsl:template&gt;

&lt;!-- Self axis --&gt;
&lt;xsl:template match="president[division='International']"&gt;
   &lt;xsl:for-each select="descendant::name"&gt;
      &lt;h2&gt;&lt;xsl:value-of select="self::*"/&gt;&lt;/h2&gt;
   &lt;/xsl:for-each&gt;
&lt;/xsl:template&gt;

&lt;!-- Descendant-or-self axis --&gt;
&lt;xsl:template match="name[.='Steve Masters']"&gt;
   &lt;h2&gt;Steve Masters and descendant elements:&lt;/h2&gt;
   &lt;table border="1"&gt;
      &lt;tr&gt;
         &lt;th&gt;Name&lt;/th&gt;
         &lt;th&gt;Emp ID&lt;/th&gt;
         &lt;th&gt;Empl Date&lt;/th&gt;
      &lt;/tr&gt;
      &lt;xsl:for-each select="parent::*/descendant-or-self::name"&gt;
         &lt;tr&gt;
            &lt;td&gt;&lt;xsl:value-of select="."/&gt;&lt;/td&gt;
            &lt;td&gt;&lt;xsl:value-of select="parent::*/@empID"/&gt;&lt;/td&gt;
            &lt;td&gt;&lt;xsl:value-of select="parent::*/@empdate"/&gt;&lt;/td&gt;
         &lt;/tr&gt;
      &lt;/xsl:for-each&gt;
   &lt;/table&gt;
&lt;/xsl:template&gt;

&lt;!-- Ancestor-or-self axis --&gt;
&lt;xsl:template match="name[.='Josh Barnhill']"&gt;
   &lt;h2&gt;Josh Barnhill's reporting structure:&lt;/h2&gt;
   &lt;table border="1"&gt;
      &lt;tr&gt;
         &lt;th&gt;Generation&lt;/th&gt;
         &lt;th&gt;Name&lt;/th&gt;
         &lt;th&gt;Emp ID&lt;/th&gt;
         &lt;th&gt;Empl Date&lt;/th&gt;
      &lt;/tr&gt;
      &lt;xsl:for-each select="ancestor-or-self::*[name()!='name']"&gt;
         &lt;tr&gt;
            &lt;td align="center"&gt;
               &lt;xsl:value-of select="position()-last()"/&gt;
            &lt;/td&gt;
            &lt;td&gt;&lt;xsl:value-of select="name"/&gt;&lt;/td&gt;
            &lt;td&gt;&lt;xsl:value-of select="@empID"/&gt;&lt;/td&gt;
            &lt;td&gt;&lt;xsl:value-of select="@empdate"/&gt;&lt;/td&gt;
         &lt;/tr&gt;
      &lt;/xsl:for-each&gt;
   &lt;/table&gt;
&lt;/xsl:template&gt;

&lt;/xsl:stylesheet&gt;</PRE>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
</BODY>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script></html>
