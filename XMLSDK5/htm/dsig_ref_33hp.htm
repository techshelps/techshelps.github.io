<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script><TITLE>Visual Basic Source: KeyFromHMACSecret.frm</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/xmlsdk.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannertitle">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannertitle" nowrap>Microsoft XML Core Services (MSXML) 5.0 for Microsoft Office - Digital Signatures</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="dsigexamples_keyfromhmacsecretfrm"></A>Visual Basic Source: KeyFromHMACSecret.frm</H1>

<PRE class="code">Dim xmldoc As New DOMDocument50
Dim xmldsig As New MXDigitalSignature50
Dim dsigKey As IXMLDSigKey
Dim DSIGNS, sigOut, sigTmp As String
Const KEYVALUE = 1

Private Function LoadXML(ByVal file As String)
    ' Read the input xml file and display the content in the text3.
    Path = App.Path + "\" + file
    xmldoc.async = False
    xmldoc.preserveWhiteSpace = True
    xmldoc.validateOnParse = False
    If xmldoc.Load(Path) = False Then
        Text1.Text = Text1.Text + "can't load " + Path + vbNewLine _
                   + xmldoc.parseError.reason + vbNewLine
        LoadXML = False
        Exit Function
    End If
    xmldoc.setProperty "SelectionNamespaces", DSIGNS
    Set xmldsig.signature = xmldoc.selectSingleNode(".//ds:Signature")
    
    LoadXML = True
End Function
Private Function SignXML(ByVal secret As String, ByVal length As Long)
    If xmldsig.signature Is Nothing Then
        Text1.Text = Text1.Text + "Invalid signature template."
        SignXML = False
        Exit Function
    End If
    
    Set oKey = xmldsig.createKeyFromHMACSecret(secret, length)
    If oKey Is Nothing Then
        Text1.Text = Text1.Text + _
            "Failed to create key from HMAC secret value\n"
        SignXML = False
        Exit Function
    End If
    
    Set oSignedKey = xmldsig.sign(oKey, KEYVALUE)
    If oSignedKey Is Nothing Then
        Text1.Text = Text1.Text + "sign failed." + vbNewLine
        SignXML = False
        Exit Function
    End If

    xmldoc.save sigOut

    Text1.Text = Text1.Text _
            + "The data referenced in the signature template " _
            + "was signed successfully." + vbNewLine _
            + "Resultant signature:" + vbNewLine + vbNewLine _
            + xmldoc.xml + vbNewLine
    SignXML = True
End Function

Private Function VerifyXML(ByVal secret As String, ByVal length As Long)
    If xmldsig.signature Is Nothing Then
        Text1.Text = Text1.Text + "Invalid signature object."
        VerifyXML = False
        Exit Function
    End If
    
    Set oKey = xmldsig.createKeyFromHMACSecret(secret, length)
    If oKey Is Nothing Then
        Text1.Text = Text1.Text + _
               "Failed to create key from HMAC secret value\n"
        VerifyXML = False
        Exit Function
    End If
    
    Set oVerifyKey = xmldsig.verify(oKey)
    If oVerifyKey Is Nothing Then
        Text1.Text = Text1.Text + "verify failed." + vbNewLine
        VerifyXML = False
        Exit Function
    End If
    
    Text1.Text = Text1.Text _
            + "The data referenced in the signature object " _
            + "was verified successfully." + vbNewLine + vbNewLine
    
    VerifyXML = True
End Function

Private Sub Form_Load()

    ' Resize the text box control to the size of the form
    Text1.Top = 100
    Text1.Left = 100
    Text1.Width = Form1.Width - 350
    Text1.Height = Form1.Height - 750

    DSIGNS = "xmlns:ds='http://www.w3.org/2000/09/xmldsig#'"
    sigTmp = "signature-template-enveloping-hmac-sha1.xml"
    sigOut = "signature-enveloping-hmac-sha1.xml"
    hmacSecret = "c2VjcmV0"
    hmacLength = -1
    Text1.Text = ""
    
    If LoadXML(sigTmp) = False Then
       Exit Sub
    End If
    
    If SignXML(hmacSecret, hmacLength) = False Then
       Exit Sub
    End If
    
    If VerifyXML(hmacSecret, hmacLength) = False Then
       Exit Sub
    End If
End Sub

Private Sub Form_Resize()
    ' Resize the text box control to the size of the form
    Text1.Width = Form1.Width - 350
    Text1.Height = Form1.Height - 750
End Sub</PRE>

<P class="labelproc"><B>Try It!</B>

<OL type="1">
	<LI>Ensure that you have completed all the procedures in <A HREF="dsig_guide_4mjo.htm">Getting Started with XML Digital Signatures</A>.</li>

	<LI>Copy the signature template <A HREF="dsig_ref_6011.htm">resource file</A>, and paste it into a text editor. Save the file as <B>signature-template-enveloping-hmac-sha1.xml</B>.</li>

	<LI>Create a Standard EXE project in Visual Basic. Save the empty project as <B>KeyFromHMACSecret.vbp</B> to the same directory where you saved <B>signature-template-enveloping-hmac-sha1.xml</B>. Name the form file <B>KeyFromHMACSecret.frm</B>. </li>

	<LI>Create a reference to MSXML 5.0. To do this, select <B>References...</B> from the <B>Project</B> menu, and then check the box for <B>Microsoft XML, v5.0</B>.</li>

	<LI>Copy the Visual Basic code listing above, and paste it into the code editor to replace whatever code is already there.</li>

	<LI>Place a <B>TextBox</B> control onto the form. It should be named as <B>Text1 </B>(the default name)<B>.</B> Click it to select it in the Properties window, and then modify the following properties for Text1: set the <B>ScrollBars</B> property to "2 &#0150; Vertical" and the <B>MultiLine</B> Property to True.</li>

	<LI>Execute the code by selecting <B>Start</B> from the <B>Run</B> menu.</li>

	<LI>Verify that the output is similar to that listed in the <A HREF="dsig_ref_90hg.htm">Output</A> topic. You can resize the form to more easily view the output.</li>
</OL>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
</BODY>
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-83731338-2"></script><script>  window.dataLayer = window.dataLayer || [];  function gtag(){dataLayer.push(arguments);}  gtag('js', new Date());  gtag('config', 'UA-83731338-2');</script></html>
