<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>Groups and Users Append, ChangePassword Methods Example (VB)</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/samples.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannerrow1">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannerparthead td" nowrap>ADOX 2.5</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="admscgroupsusersappendchangepasswordmethodsexamplex"></A>Groups and Users Append, ChangePassword Methods Example (VB)</H1>

<P>This example demonstrates the <A HREF="admthappendgroups.htm">Append</A> method of <A HREF="adcolgroups.htm">Groups</A>, as well as the <A HREF="admthappendusers.htm">Append</A> method of <A HREF="adcolusers.htm">Users</A> by adding a new <A HREF="adobjgroup.htm">Group</A> and a new <A HREF="adobjuser.htm">User</A> to the system. The new <B>Group</B> is appended to the <B>Groups</B> collection of the new <B>User</B>. Consequently, the new <B>User</B> is added to the <B>Group</B>. Also, the <A HREF="admthchangepassword.htm">ChangePassword</A> method is used to specify the <B>User</B> password.</P>

<PRE class="code">' BeginGroupVB
Sub Main()
    On Error GoTo GroupXError

    Dim cat As ADOX.Catalog
    Dim usrNew As ADOX.User
    Dim usrLoop As ADOX.User
    Dim grpLoop As ADOX.Group
    
    Set cat = New ADOX.Catalog
    
    cat.ActiveConnection = "Provider='Microsoft.Jet.OLEDB.4.0';" &amp; _
        "Data Source='c:\Program Files\" &amp; _
        "Microsoft Office\Office\Samples\Northwind.mdb';" &amp; _
        "jet oledb:system database=" &amp; _
        "'c:\Program Files\Microsoft Office\Office\system.mdw'"

    With cat
        'Create and append new group with a string.
        .Groups.Append "Accounting"
       
        ' Create and append new user with an object.
        Set usrNew = New ADOX.User
        usrNew.Name = "Pat Smith"
        usrNew.ChangePassword "", "Password1"
        .Users.Append usrNew

        ' Make the user Pat Smith a member of the
        ' Accounting group by creating and adding the
        ' appropriate Group object to the user's Groups
        ' collection. The same is accomplished if a User
        ' object representing Pat Smith is created and
        ' appended to the Accounting group Users collection
        usrNew.Groups.Append "Accounting"
      
        ' Enumerate all User objects in the
        ' catalog's Users collection.
        For Each usrLoop In .Users
            Debug.Print "  " &amp; usrLoop.Name
            Debug.Print "    Belongs to these groups:"
            ' Enumerate all Group objects in each User
            ' object's Groups collection.
            If usrLoop.Groups.Count &lt;&gt; 0 Then
                For Each grpLoop In usrLoop.Groups
                    Debug.Print "    " &amp; grpLoop.Name
                Next grpLoop
            Else
                Debug.Print "    [None]"
            End If
        Next usrLoop

        ' Enumerate all Group objects in the default
        ' workspace's Groups collection.
        For Each grpLoop In .Groups
            Debug.Print "  " &amp; grpLoop.Name
            Debug.Print "    Has as its members:"
            ' Enumerate all User objects in each Group
            ' object's Users collection.
            If grpLoop.Users.Count &lt;&gt; 0 Then
                For Each usrLoop In grpLoop.Users
                    Debug.Print "    " &amp; usrLoop.Name
                Next usrLoop
            Else
                Debug.Print "    [None]"
            End If
        Next grpLoop
        
        ' Delete new User and Group objects because this
        ' is only a demonstration.
        ' These two line are commented out because the sub "OwnersX" uses
        ' the group "Accounting".
'        .Users.Delete "Pat Smith"
'        .Groups.Delete "Accounting"

    End With

    'Clean up
    Set cat.ActiveConnection = Nothing
    Set cat = Nothing
    Set usrNew = Nothing
    Exit Sub
    
GroupXError:
    
    Set cat = Nothing
    Set usrNew = Nothing
    
    If Err &lt;&gt; 0 Then
        MsgBox Err.Source &amp; "--&gt;" &amp; Err.Description, , "Error"
    End If
End Sub
' EndGroupVB</PRE>

<H4 class="dtH4">See Also</H4>

<P><A HREF="admthappendgroups.htm">Append Method (Groups)</A> | <A HREF="admthappendusers.htm">Append Method (Users)</A> | <A HREF="adobjcatalog.htm">Catalog Object</A> | <A HREF="admthchangepassword.htm">ChangePassword Method</A> | <A HREF="adobjgroup.htm">Group Object</A> | <A HREF="adcolgroups.htm">Groups Collection</A> | <A HREF="adobjuser.htm">User Object</A> | <A HREF="adcolusers.htm">Users Collection</A></P>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
<P align="center"><A HREF="ado_sdk_copyright_and_trademarks.htm">&copy; 1998-2003 Microsoft Corporation. All rights reserved.</A></P>
</BODY>
</HTML>
