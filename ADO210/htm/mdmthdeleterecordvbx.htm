<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>DeleteRecord and MoveRecord Methods Example (VB)</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/samples.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannerrow1">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannerparthead td" nowrap>ADO 2.5 Samples</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="mdmthdeleterecordvbx"></A>DeleteRecord and MoveRecord Methods Example (VB)</H1>

<P>This example demonstrates how to copy, move, edit, and delete the contents of a text file published to a Web folder. Other properties and methods used include <A HREF="mdmthgetchildrenmethodado.htm" CLASS="POPUP">GetChildren</A>, <A HREF="mdproparenturl.htm" CLASS="POPUP">ParentURL</A>, <A HREF="mdprosourcerecord.htm" CLASS="POPUP">Source</A>, and <A HREF="mdmthflush.htm" CLASS="POPUP">Flush</A>.</P>

<PRE class="code">'BeginDeleteRecordVB

'Note:
' IIS must be running for this sample to work. To
' use this sample you must:
'
' 1. create folders named "test" and "test2"
'    in the root web folder of http://MyServer
'
' 2. Create a text file named "test2.txt" in the
'    "test" folder.
' 3. Replace "MyServer" with the appropriate web
'    server name.
    
Public Sub Main()
    On Error GoTo ErrorHandler

     ' connection and recordset variables
    Dim rsDestFolder As ADODB.Recordset
    Dim Cnxn As ADODB.Connection
    Dim strCnxn As String
 
     ' file as record variables
    Dim rcFile As ADODB.Record
    Dim rcDestFile As ADODB.Record
    Dim rcDestFolder As ADODB.Record
    Dim objStream As Stream
    
     ' file variables
    Dim strFile As String
    Dim strDestFile As String
    Dim strDestFolder As String
            
     ' instantiate variables
    Set rsDestFolder = New ADODB.Recordset
    Set rcDestFolder = New ADODB.Record
    Set rcFile = New ADODB.Record
    Set rcDestFile = New ADODB.Record
    Set objStream = New ADODB.Stream
     
     ' open a record on a text file
    Set Cnxn = New ADODB.Connection
    strCnxn = "url=http://MyServer/"
    Cnxn.Open strCnxn
    strFile = "test/test2.txt"
    rcFile.Open strFile, Cnxn, adModeReadWrite, adOpenIfExists Or adCreateNonCollection
    Debug.Print Cnxn
    
     ' edit the contents of the text file
    objStream.Open rcFile, , adOpenStreamFromRecord
    
    Debug.Print "Source: " &amp; strCnxn &amp; rcFile.Source
    Debug.Print "Original text: " &amp; objStream.ReadText
    
    objStream.Position = 0
    objStream.WriteText "Newer Text. "
    objStream.Position = 0
    
    Debug.Print "New text: " &amp; objStream.ReadText
    
     ' reset the stream object
    objStream.Flush
    objStream.Close
    rcFile.Close
    
     ' reopen record to see new contents of text file
    rcFile.Open strFile, Cnxn, adModeReadWrite, adOpenIfExists Or adCreateNonCollection
    objStream.Open rcFile, adModeReadWrite, adOpenStreamFromRecord
    
    Debug.Print "Source: " &amp; strCnxn &amp; rcFile.Source
    Debug.Print "Edited text: " &amp; objStream.ReadText
    
     ' copy the file to another folder
    strDestFile = "test2/test1.txt"
    rcFile.CopyRecord "", "http://MyServer/" &amp; strDestFile, "", "", adCopyOverWrite
    
     ' delete the original file
    rcFile.DeleteRecord
    
     ' move the file from the subfolder back to original location
    strDestFolder = "test2/"
    rcDestFolder.Open strDestFolder, Cnxn ', adOpenIfExists  'Or adCreateCollection
    Set rsDestFolder = rcDestFolder.GetChildren
    rsDestFolder.MoveFirst
    
     ' position current record at on the correct file
    Do While Not (rsDestFolder.EOF Or rsDestFolder(0) = "test1.txt")
        rsDestFolder.MoveNext
    Loop
    
     ' open a record on the correct row of the recordset
    rcDestFile.Open rsDestFolder, Cnxn
    
     ' do the move
    rcDestFile.MoveRecord "", "http://MyServer/" &amp; strFile, "", "", adMoveOverWrite
    
    ' clean up
    rsDestFolder.Close
    Cnxn.Close
    Set rsDestFolder = Nothing
    Set Cnxn = Nothing
    Exit Sub
    
ErrorHandler:
    ' clean up
    If Not rsDestFolder Is Nothing Then
        If rsDestFolder.State = adStateOpen Then rsDestFolder.Close
    End If
    Set rsDestFolder = Nothing
    
    If Not Cnxn Is Nothing Then
        If Cnxn.State = adStateOpen Then Cnxn.Close
    End If
    Set Cnxn = Nothing
    
    If Err &lt;&gt; 0 Then
        MsgBox Err.Source &amp; "--&gt;" &amp; Err.Description, , "Error"
    End If
End Sub
'EndDeleteRecordVB</PRE>

<H4 class="dtH4">See Also</H4>

<P><A HREF="mdmthdeleterecord.htm">DeleteRecord Method</A> | <A HREF="mdmthflush.htm">Flush Method</A> | <A HREF="mdmthgetchildrenmethodado.htm">GetChildren Method</A> | <A HREF="mdmthmoverecord.htm">MoveRecord Method</A> | <A HREF="mdproparenturl.htm">ParentURL Property</A> | <A HREF="mdprosourcerecord.htm">Source Property (ADO Record)</A></P>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
<P align="center"><A HREF="ado_sdk_copyright_and_trademarks.htm">&copy; 1998-2003 Microsoft Corporation. All rights reserved.</A></P>
</BODY>
</HTML>
