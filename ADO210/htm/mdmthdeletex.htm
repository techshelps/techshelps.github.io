<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>Delete Method Example (VB)</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/samples.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannerrow1">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannerparthead td" nowrap>ADO 2.5 Samples</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="mdmthdeletex"></A>Delete Method Example (VB)</H1>

<P>This example uses the <A HREF="mdmthrstdelete.htm">Delete</A> method to remove a specified record from a <A HREF="mdobjodbrec.htm">Recordset</A>.</P>

<PRE class="code">'BeginDeleteVB

    'To integrate this code
    'replace the data source and initial catalog values
    'in the connection string

Public Sub Main()
    On Error GoTo ErrorHandler

    Dim rstRoySched As ADODB.Recordset
    Dim Cnxn As ADODB.Connection
    Dim strCnxn As String
    Dim strSQLRoySched As String
    
    Dim strMsg As String
    Dim strTitleID As String
    Dim intLoRange As Integer
    Dim intHiRange As Integer
    Dim intRoyalty As Integer
    
     ' open connection
    Set Cnxn = New ADODB.Connection
    strCnxn = "Provider='sqloledb';Data Source='MySqlServer';" &amp; _
        "Initial Catalog='Pubs';Integrated Security='SSPI';"
    Cnxn.Open strCnxn
    
    ' open RoySched table with cursor client-side
    Set rstRoySched = New ADODB.Recordset
    rstRoySched.CursorLocation = adUseClient
    rstRoySched.CursorType = adOpenStatic
    rstRoySched.LockType = adLockBatchOptimistic
    rstRoySched.Open "SELECT * FROM roysched WHERE royalty = 20", strCnxn, , , adCmdText
    
    ' Prompt for a record to delete
    strMsg = "Before delete there are " &amp; rstRoySched.RecordCount &amp; _
       " titles with 20 percent royalty:" &amp; vbCr &amp; vbCr
    
    Do While Not rstRoySched.EOF
       strMsg = strMsg &amp; rstRoySched!title_id &amp; vbCr
       rstRoySched.MoveNext
    Loop
    
    strMsg = strMsg &amp; vbCr &amp; vbCr &amp; "Enter the ID of a record to delete:"
    strTitleID = UCase(InputBox(strMsg))
    
    If strTitleID = "" Then
        Err.Raise 1, , "You didn't enter any value for the record ID."
    End If
    
    ' Move to the record and save data so it can be restored
    rstRoySched.Filter = "title_id = '" &amp; strTitleID &amp; "'"
    
    If rstRoySched.RecordCount &lt; 1 Then
        Err.Raise 1, , "There is no record for the record ID you entered."
    End If
    
    intLoRange = rstRoySched!lorange
    intHiRange = rstRoySched!hirange
    intRoyalty = rstRoySched!royalty
    
    ' Delete the record
    rstRoySched.Delete
    rstRoySched.UpdateBatch
    
    ' Show the results
    rstRoySched.Filter = adFilterNone
    rstRoySched.Requery
    strMsg = ""
    strMsg = "After delete there are " &amp; rstRoySched.RecordCount &amp; _
       " titles with 20 percent royalty:" &amp; vbCr &amp; vbCr
    Do While Not rstRoySched.EOF
        strMsg = strMsg &amp; rstRoySched!title_id &amp; vbCr
        rstRoySched.MoveNext
    Loop
    MsgBox strMsg
    
    ' Restore the data because this is a demonstration
    rstRoySched.AddNew
    rstRoySched!title_id = strTitleID
    rstRoySched!lorange = intLoRange
    rstRoySched!hirange = intHiRange
    rstRoySched!royalty = intRoyalty
    rstRoySched.UpdateBatch

    ' clean up
    rstRoySched.Close
    Set rstRoySched = Nothing
    Exit Sub
    
ErrorHandler:
    ' clean up
    If Not rstRoySched Is Nothing Then
        If rstRoySched.State = adStateOpen Then rstRoySched.Close
    End If
    Set rstRoySched = Nothing
    
    If Err &lt;&gt; 0 Then
        MsgBox Err.Source &amp; "--&gt;" &amp; Err.Description, , "Error"
    End If
End Sub
'EndDeleteVB</PRE>

<H4 class="dtH4">See Also</H4>

<P><A HREF="mdmthrstdelete.htm">Delete Method (ADO Recordset)</A> | <A HREF="mdobjodbrec.htm">Recordset Object</A></P>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
<P align="center"><A HREF="ado_sdk_copyright_and_trademarks.htm">&copy; 1998-2003 Microsoft Corporation. All rights reserved.</A></P>
</BODY>
</HTML>
