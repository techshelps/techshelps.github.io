<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML DIR="LTR"><HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>OriginalValue and UnderlyingValue Properties Example (VB)</TITLE>
<SCRIPT SRC="../stylesheets/vs70link.js"></SCRIPT>
<SCRIPT SRC="../stylesheets/vs70.js"></SCRIPT>
<SCRIPT LANGUAGE="JScript" SRC="../stylesheets/samples.js"></SCRIPT></HEAD>
<body topmargin=0 id="bodyID" class = "dtBODY">

<div id="scrbanner">
<div id="bannerrow1">
<TABLE CLASS="bannerparthead" CELLSPACING=0>
<TR ID="hdr">
<TD CLASS="bannerparthead td" nowrap>ADO 2.5 Samples</TD><TD valign=middle><a href="#Feedback"><IMG name="feedb" onclick=startFeedback(SDKFeedB) style="CURSOR: hand;" hspace=15 alt="" src="../stylesheets/mailto.gif" align=right></a></TD>
</TR>
</TABLE>
</div>
</div>
<DIV id="scrtext" valign="bottom">
<H1 class="dtH1"><A NAME="mdprooriginalvaluex"></A>OriginalValue and UnderlyingValue Properties Example (VB)</H1>

<P>This example demonstrates the <A HREF="mdprooriginalvalue.htm">OriginalValue</A> and <A HREF="mdprounderlyingvalue.htm">UnderlyingValue</A> properties by displaying a message if a record's underlying data has changed during a <A HREF="mdobjodbrec.htm">Recordset</A> batch update.</P>

<PRE class="code">'BeginOriginalValueVB
Public Sub Main()
    On Error GoTo ErrorHandler

    'To integrate this code
    'replace the data source and initial catalog values
    'in the connection string

    Dim Cnxn As ADODB.Connection
    Dim rstTitles As ADODB.Recordset
    Dim fldType As ADODB.Field
    Dim strCnxn As String
    Dim strSQLTitles As String
    
    ' Open connection.
    Set Cnxn = New ADODB.Connection
    strCnxn = "Provider='sqloledb';Data Source='MySqlServer';" &amp; _
        "Initial Catalog='Pubs';Integrated Security='SSPI';"
    Cnxn.Open strCnxn
    
    ' Open recordset for batch update
    ' using object refs to set properties
    Set rstTitles = New ADODB.Recordset
    Set rstTitles.ActiveConnection = Cnxn
    rstTitles.CursorType = adOpenKeyset
    rstTitles.LockType = adLockBatchOptimistic
    strSQLTitles = "titles"
    rstTitles.Open strSQLTitles
    
    ' Set field object variable for Type field
    Set fldType = rstTitles!Type
    
    ' Change the type of psychology titles
    Do Until rstTitles.EOF
        If Trim(fldType) = "psychology" Then
            fldType = "self_help"
        End If
        rstTitles.MoveNext
    Loop
    
    ' Similate a change by another user by updating
    ' data using a command string
    Cnxn.Execute "UPDATE Titles SET type = 'sociology' " &amp; _
       "WHERE type = 'psychology'"
    
    'Check for changes
    rstTitles.MoveFirst
    Do Until rstTitles.EOF
        If fldType.OriginalValue &lt;&gt; fldType.UnderlyingValue Then
            MsgBox "Data has changed!" &amp; vbCr &amp; vbCr &amp; _
                "  Title ID: " &amp; rstTitles!title_id &amp; vbCr &amp; _
                "  Current value: " &amp; fldType &amp; vbCr &amp; _
                "  Original value: " &amp; _
                fldType.OriginalValue &amp; vbCr &amp; _
                "  Underlying value: " &amp; _
                fldType.UnderlyingValue &amp; vbCr
        End If
    rstTitles.MoveNext
    Loop
    
    ' Cancel the update because this is a demonstration
    rstTitles.CancelBatch
    
    ' Restore original values
    Cnxn.Execute "UPDATE Titles SET type = 'psychology' " &amp; _
        "WHERE type = 'sociology'"
   
    ' clean up
    rstTitles.Close
    Cnxn.Close
    Set rstTitles = Nothing
    Set Cnxn = Nothing
    Exit Sub
    
ErrorHandler:
    ' clean up
    If Not rstTitles Is Nothing Then
        If rstTitles.State = adStateOpen Then rstTitles.Close
    End If
    Set rstTitles = Nothing
    
    If Not Cnxn Is Nothing Then
        If Cnxn.State = adStateOpen Then Cnxn.Close
    End If
    Set Cnxn = Nothing
    
    If Err &lt;&gt; 0 Then
        MsgBox Err.Source &amp; "--&gt;" &amp; Err.Description, , "Error"
    End If
End Sub
'EndOriginalValueVB</PRE>

<H4 class="dtH4">See Also</H4>

<P><A HREF="mdprooriginalvalue.htm">OriginalValue Property</A> | <A HREF="mdobjodbrec.htm">Recordset Object</A> | <A HREF="mdprounderlyingvalue.htm">UnderlyingValue Property</A></P>
<H4><A NAME="feedback"></A></H4>
<SPAN id="SDKFeedB"></SPAN>
</div>
<P align="center"><A HREF="ado_sdk_copyright_and_trademarks.htm">&copy; 1998-2003 Microsoft Corporation. All rights reserved.</A></P>
</BODY>
</HTML>
